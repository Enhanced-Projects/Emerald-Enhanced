//quests for outcasts


script(global) RyuOutcastsDailyQuest  {
    lock
    faceplayer
     if (flag(FLAG_FINISHED_OUTCAST_DAILY) == TRUE) { //quest done
        checkfactionstanding(FACTION_OUTCASTS, 175)
        if ((var(VAR_RESULT) == TRUE) && (flag(FLAG_RYU_OUTCASTS_SPECIAL_STARTED) == FALSE)) {
            call(RyuOutcastsSpecialQuestIntro)
        }
        msgbox("The rituals are going as intended.")
        release
        end
    }

    checkfactionstanding(FACTION_OUTCASTS, 200)

    if ((var(VAR_RESULT) == TRUE) && (flag(FLAG_FINISHED_OUTCAST_DAILY) == TRUE)) {
        msgbox("Archmage, I'm pleased to see you.\pHow are the leylines treating you\lthis day?")
        closemessage
    }

    if (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_OUTCASTS) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) { //Also put the new quest conclusion here. This is where it checks if the quest is complete. If so, sends to the common reward script.
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonOutcastRewardScript)
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("Yo... we still need {STR_VAR_1} {STR_VAR_2}.")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                release
                end
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("I see. It was a {STR_VAR_1}. It doesn't appear to have any psychic power. You can keep it."))
                    goto(CommonOutcastRewardScript)
                    release
                    end
                }
                else {
                    msgbox(format("We still await the hatching of that potential scion."))
                    release
                    end
                }
        }

    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_OUTCASTS) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_OUTCASTS)
        callnative(RyuSummonOriginalNPCscript)
        end
    }

    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_OUTCASTS)
    random(3)//increase this by the number of quests you add.
    if (flag(FLAG_RYU_VERBOSE_MODE) == TRUE) {
        call(RyuDoDailyQuestVerbosePrint)
    }
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_ENERGY_POWDER)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 5)
            msgbox(OutcastsQuest1Desc)
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_STARDUST)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 5)
            msgbox("Heya {PLAYER}.\pWe recently got word that some\nrare pokemon can be attracted\lby anointing stones with stardust.\pPlease bring us five pouches of it.")
        case 2:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(format("This egg has an impressive aura. Could you hatch it to see what it turns out to be?"))
            closemessage
            msgbox(FactionsReceivedEggMsg)
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    call_if_unset(FLAG_RYU_FACTION_TUTORIAL_UNLOCKED, RyuScript_FactionsTutorialQuery)
    closemessage
    release
    end
}

text OutcastsQuest1Desc {
    "Greetings, familiar stranger.\p"
    "We're trying to conduct a ritual\n"
    "that will summon more rare\l"
    "pokemon.\p"
    "Can you bring us some Energy Powder?\n"
    "five portions should do."
}

script CommonOutcastRewardScript {
    msgbox("The gods shine upon you\nthis day.\lMany thanks.")
    setflag(FLAG_FINISHED_OUTCAST_DAILY)
    changefactionstanding(FACTION_OUTCASTS, 25, FALSE)
    random(1500)
    addvar(VAR_RESULT, 3000)
    buffernumberstring(0, VAR_RESULT)
    addmoney(VAR_RESULT)
    goto(CommonDailyQuestEnd)
}






//special events

script RyuOutcastsSpecialQuestIntro {
    goto_if_set(FLAG_RYU_OUTCASTS_SPECIAL_STARTED, RyuOutcastsSpecialAlreadyInProgress)
    msgbox(AlchemyIntroQuestMsg)
    closemessage
    setflag(FLAG_RYU_OUTCASTS_SPECIAL_STARTED)
    release
    end
}

script RyuOutcastsSpecialAlreadyInProgress {
    goto(ryu_return)
}

text AlchemyIntroQuestMsg {
    "Hello, {PLAYER}! Good news!\n"
    "We found a practitioner of\l"
    "the lost art of Alchemy!\p"
    "Unfortunately, she refuses\n"
    "to let any of us in her home!\p"
    "Surely she will let you in,\n"
    "as you're the strongest\l"
    "member of the Outcasts!\p"
    "You have to go see lady\n"
    "Lorwyn.\p"
    "You can find her house near\n"
    "Fortree City.\p"
    "She likes to be high up,\n"
    "close to the sky."
}

script RyuAlchemy_LorwynsDoor {
    checkfactionstanding(FACTION_OUTCASTS, 175)
    compare(VAR_RESULT, FALSE)
    goto_if_eq(Route119_EventScript_DoorIsLocked)//don't let player in if they don't have enough faction standing
    goto_if_unset(FLAG_RYU_OUTCASTS_SPECIAL_STARTED, Route119_EventScript_DoorIsLocked)//don't let player in if they haven't heard about lady Lorwyn
    msgbox(format("{COLOR LIGHT_GREEN}{SHADOW GREEN}There's some archaic, ancient looking text on the door. You can't make heads or tails of it. As you were focusing on the characters, the door opens and starts to pull you in."))
    closemessage
    warpdoor(MAP_ROUTE119_LORWYNS_HOUSE, 0, 3, 5)
    waitstate
}

script RyuAlchemy_LadyLorwyn {//this lady will provide transportation to the lost city.
    lock
    faceplayer
    checkfactionstanding(FACTION_OUTCASTS, 175)//How could people get here anyway, if they aren't high enough? Just in case check.
    compare(VAR_RESULT, FALSE)
    goto_if_eq(RA_Lorwyn_NeedsFactionStanding)
    compare(VAR_RYU_PLAYER_MINING_SKILL, 2)
    goto_if_lt(RA_Lorwyn_NeedsMining)
    compare(VAR_RYU_PLAYER_BOTANY_SKILL, 3)
    goto_if_lt(RA_Lorwyn_NeedsBotany)
    goto_if_set(FLAG_RYU_FINISHED_LORWYNS_TEST, RA_Lorwyn_DoTeleport)
    msgbox(format("Hello, {PLAYER}. The lessers tell me that they think you are worthy of my tutelage. We shall see about that!"))
    setflag(FLAG_RYU_BOSS_SCALE)
    setflag(FLAG_RYU_FACING_FACTION_BOSS)
    trainerbattle_single(TRAINER_LORWYN, LorwynBattleIntro, LorwynFinishedBattle, RA_Lorwyn_DoTeleport)
    waitstate
}

text LorwynBattleIntro {
    "I hope you're prepared to\nface the power of the\lancients!$"
}

text LorwynFinishedBattle {
    "Very impressive!\nMy ancients were of no contest!$"
}

script RA_Lorwyn_NeedsFactionStanding {
    msgbox(format("What are you doing here?\pLeave, or I will torture you with a Voodoo doll."))
    closemessage
    release
    end
}

script RA_Lorwyn_NeedsMining {
    msgbox(format("You need to be more in tune with the Earth before I will divulge anything to you.\p {COLOR LIGHT_GREEN}{SHADOW GREEN}(You should increase your mining skill further.)"))
    closemessage
    release
    end
}

script RA_Lorwyn_NeedsBotany {
    msgbox(format("You are not yet one with nature. Return when your knowledge of nature has expanded.\p {COLOR LIGHT_GREEN}{SHADOW GREEN}(You should increase your botany skill.)"))
    closemessage
    release
    end
}

script RA_Lorwyn_DoTeleport {
    setflag(FLAG_RYU_FINISHED_LORWYNS_TEST)
    msgbox(format("I believe that there is a link between Basalt Burg and Snowy Shore. The sign in the middle of both towns is one and the same."))
    closemessage
    msgbox(format("Would you like to know more about Basalt Burg?"), MSGBOX_YESNO)
    compare(VAR_RESULT, YES)
    call_if_eq(RA_Lorwyn_ExplainVillage)
    msgbox(format("I'm now prepared to send you to the hidden village."))
    closemessage
    drawfullscreenimage(SCENEBGWARP)
    delay(60)
    clearfullscreenimage
    warpteleport(MAP_SNOWY_SHORE_DARK_WORLD, 255, 20, 16)//put the new map name here.
    waitstate
}

script RA_Lorwyn_ExplainVillage {
    msgbox(format("A long time ago, Alchemy was one of the most rewarding professions."
                  "If you could do Alchemy, people from all over the world would seek you "
                  "out for miracle cures, or performance boosts. However, Alchemists were "
                  "typically burned at the stake if they were caught by the guards of certain "
                  "kingdoms. Because of this a magical barrier was forged around the outskirts "
                  "of the town, to make it look like nothing was there. As a result, this village "
                  "was able to exist indefinitely in its time period. However, all of the occupants "
                  "have long since died out. The only way to visit this place now, is via a special "
                  "time travel spell that was almost lost to time. "
                  "To my knowledge, I am the only one who can perform this spell."))
    closemessage
    return
}
