//Quests for Students


script(global) RyuStudentsDailyQuest  {
    lock
    faceplayer
     if (flag(FLAG_FINISHED_STUDENT_DAILY) == TRUE) { //quest done
        msgbox("We don't have any requests\nright now!")
        release
        end
    }

    checkfactionstanding(FACTION_STUDENTS, 200)

    if (var(VAR_RESULT) == TRUE) {
        msgbox("Hey Boss! How's it going?\nThe Council is pleased with how\lthings are going!")
        release
        end
    }

    if (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_STUDENTS) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) {
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonStudentRewardScript)
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("The student council still wants \n{STR_VAR_1} {STR_VAR_2}.")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                if (var(VAR_RYU_DAILY_QUEST_DATA) == 4000) {
                    msgbox(format("I see, competition is pretty fierce out there, huh?"))
                    goto(CommonStudentRewardScript)
                }
                else {
                    buffermapname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Go scout the competition at {STR_VAR_1}, already."))
                    release
                    end
                }
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Aha! it was a {STR_VAR_1}! How cute. Please take care of it!"))
                    goto(CommonStudentRewardScript)
                    release
                    end
                }
                else {
                    msgbox(format("Hurry and hatch that egg, already!"))
                    release
                    end
                }
            }
    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_STUDENTS) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_STUDENTS)
        callnative(RyuSummonOriginalNPCscript)
        end
    }

    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_STUDENTS)
    random(5)//increase this by the number of quests you add.
    if (flag(FLAG_RYU_VERBOSE_MODE) == TRUE) {
        call(RyuDoDailyQuestVerbosePrint)
    }
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_POTION)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 5)
            msgbox("Sup {PLAYER}!\pThe student council wants some\npotions for new members.\pWe need a total of five.")
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_POKE_BALL)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 10)
            msgbox("Heya {PLAYER}!\pThe Council wants you to get some\npoke balls for our members.\pWe need a total of ten.")
        case 2:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, TRAVEL_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, MAPSEC_ROUTE_114)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 15)
            clearflag(FLAG_RYU_STARTED_TRAVEL_TIMER)
            msgbox(format("{PLAYER}! I need you to go to Route 114 and check out our competition there."))
        case 3:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(format("One of our younger members found this egg. You can have it, but you have to show us what it hatched into!"))
            closemessage
            msgbox(FactionsReceivedEggMsg)
        case 4:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_PRETTY_WING)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 4)
            msgbox(format("Hey {PLAYER}! Great to see you here. We learned about Wingulls today and we want to examine them further. Apparently sometimes they have loose wings but we can't find any! Can you help us?"))
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    closemessage
    release
    end
}

script CommonStudentRewardScript {
    msgbox("The new kids will need all the\nhelp they can get. \pThanks, {PLAYER}!")
    setflag(FLAG_FINISHED_STUDENT_DAILY)
    changefactionstanding(FACTION_STUDENTS, 25, FALSE)
    random(1500)
    addvar(VAR_RESULT, 3000)
    buffernumberstring(0, VAR_RESULT)
    addmoney(VAR_RESULT)
    goto(CommonDailyQuestEnd)
}
