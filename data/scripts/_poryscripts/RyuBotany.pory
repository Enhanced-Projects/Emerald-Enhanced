raw `
RyuScript_FieldCut::
    setvar VAR_TEMP_5, 1
    call_if_set FLAG_BOTANY_HAS_HYPER_CUTTER, RyuBotany_DoubleRewardHC
    delay 60
    switch VAR_RYU_PLAYER_BOTANY_SKILL
    case 1, RyuBotanyGrass_GiveOneItem
    case 2, RyuBotanyGrass_GiveOneItem
    case 3, RyuBotanyGrass_TryGive2Items
    case 4, RyuBotanyGrass_TryGive2Items
    case 5, RyuBotanyGrass_TryGive3Items
    goto ryu_end
    end

RyuBotanyGiveTreeRewardItem::
    switch VAR_RYU_PLAYER_BOTANY_SKILL
    case 1, RyuBotanyTree_GiveOneItem
    case 2, RyuBotanyTree_GiveOneItem
    case 3, RyuBotanyTree_TryGive2Items
    case 4, RyuBotanyTree_TryGive2Items
    case 5, RyuBotanyTree_TryGive3Items
    goto ryu_end


@the script called when cutting trees
RyuBotany_CheckTreeRewardItem::
    random 3
    compare VAR_RESULT, 2 @25% chance to get nothing from trees
    goto_if_le RyuBotanyGiveTreeRewardItem
    release
    end

RyuBotany_DoubleRewardHC::
    random 3
    compare VAR_RESULT, 1
    goto_if_eq ryu_return
    setvar VAR_TEMP_5, 2
    return

RyuBotanyTree_GiveOneItem::
    callnative BotanyTreeChop
    giveitem VAR_TEMP_0, 1
    closemessage
    addvars VAR_RYU_PLAYER_BOTANY_SKILL_EXP, VAR_RYU_PLAYER_BOTANY_SKILL@Player gains 1 exp per level of botany per botany check.
    call RyuBotany_CheckLevelUp
    return

RyuBotanyGrass_GiveOneItem::
    callnative BotanyCheck
    setvar VAR_TEMP_5, 1
    call_if_set FLAG_BOTANY_HAS_HYPER_CUTTER, RyuBotany_DoubleRewardHC
    additem VAR_TEMP_0, VAR_TEMP_5
    bufferitemname 0, VAR_TEMP_0
    buffernumberstring 1, VAR_TEMP_5
    msgbox RyuBotany_RewardMessage
    addvars VAR_RYU_PLAYER_BOTANY_SKILL_EXP, VAR_RYU_PLAYER_BOTANY_SKILL
    addvars VAR_RYU_PLAYER_BOTANY_SKILL_EXP, VAR_RYU_PLAYER_BOTANY_SKILL
    addvars VAR_RYU_PLAYER_BOTANY_SKILL_EXP, VAR_RYU_PLAYER_BOTANY_SKILL
    call RyuBotany_CheckLevelUp
    closemessage
    return

RyuBotany_RewardMessage:
    .string "Obtained {STR_VAR_2} {STR_VAR_1}(s)$"

RyuBotanyTree_TryGive2Items::
    call RyuBotanyTree_GiveOneItem
    random 1
    compare VAR_RESULT, 1 @50% chance to get one bonus roll at rank 3
    call_if_eq RyuBotanyTree_GiveOneItem
    release
    end

RyuBotanyTree_TryGive3Items::
    call RyuBotanyTree_GiveOneItem
    random 1
    compare VAR_RESULT, 1
    call_if_eq RyuBotanyTree_GiveOneItem
    random 3
    compare VAR_RESULT, 1 @25% chance to get a 3rd bonus roll at rank 5.
    call_if_eq RyuBotanyTree_GiveOneItem
    release
    end

RyuBotanyGrass_TryGive2Items::
    call RyuBotanyGrass_GiveOneItem
    random 1
    compare VAR_RESULT, 1 @50% chance to get one bonus roll at rank 3
    call_if_eq RyuBotanyGrass_GiveOneItem
    release
    end

RyuBotanyGrass_TryGive3Items::
    call RyuBotanyGrass_GiveOneItem
    random 1
    compare VAR_RESULT, 1
    call_if_eq RyuBotanyGrass_GiveOneItem
    random 3
    compare VAR_RESULT, 1 @25% chance to get a 3rd bonus roll.
    call_if_eq RyuBotanyGrass_GiveOneItem
    release
    end

RyuBotanyRecipeBookScript::
    lock
    clearflag FLAG_RYU_DISPLAY_BOTANY_INGREDIENTS
    clearflag FLAG_RYU_DISPLAY_ALCHEMY_INGREDIENTS
    compare VAR_RYU_PLAYER_ALCHEMY_SKILL, 1  @@Alchemy shows up in the recipe book if the player has the skill, otherwise works exactly as it did before alchemy.
    goto_if_lt RyuBotany_BotanyRecipeCrafting
    msgbox WhichTypeOfRecipe
    bufferdynamicmulti RAMenu_Botany, RAMenu_Alchemy, RAMenu_HarvestLevel
    multichoice 0, 6, MULTI_DYNAMIC_3, 1
    switch VAR_RESULT
    case 0, RyuBotany_BotanyRecipeCrafting
    case 1, RyuAlchemy_AttemptCreateEffect
    case 2, RyuAlchemy_ChangeHarvestLevel
    goto ryu_end

RAMenu_Alchemy:
    .string "Alchemy$"

RAMenu_Botany:
    .string "Botany$"

RAMenu_HarvestLevel:
    .string "Harvest Level$"

RyuBotany_BotanyRecipeCrafting::
    msgbox RB_WhichChapter
    multichoice 18, 4, MULTI_BOTANY_CHAPTER_SELECT, 0
    switch VAR_RESULT
    case 0, RB_ShowConsumablesChapter
    case 1, RB_ShowMedicinesChapter
    case 2, RB_ShowEvolutionChapter
    goto ryu_end

RB_DB_MSG1:
    .string "Resulting item is {STR_VAR_1}.$"

RB_ShowConsumablesChapter::
    msgbox RB_WhichRecipe
    setflag FLAG_RYU_DISPLAY_BOTANY_INGREDIENTS
    setvar VAR_TEMP_D, 0
    scrollingmultichoice SCROLLINGMULTICHOICE_BOTANYCONSUMABLECHAPTER, 0, 2, 5, 1
    compare VAR_RESULT, 127
    goto_if_eq RyuBotanyRecipeBookScript
    trycraftingrecipe VAR_RESULT
    goto RB_CheckCraftingResult

RB_ShowMedicinesChapter::
    msgbox RB_WhichRecipe
    setflag FLAG_RYU_DISPLAY_BOTANY_INGREDIENTS
    setvar VAR_TEMP_D, 1
    scrollingmultichoice SCROLLINGMULTICHOICE_BOTANYMEDICINECHAPTER, 0, 2, 5, 1
    compare VAR_RESULT, 127
    goto_if_eq RyuBotanyRecipeBookScript
    addvar VAR_RESULT, NUM_CONSUMABLE_RECIPES
    trycraftingrecipe VAR_RESULT
    goto RB_CheckCraftingResult

RB_ShowEvolutionChapter::
    msgbox RB_WhichRecipe
    setflag FLAG_RYU_DISPLAY_BOTANY_INGREDIENTS
    setvar VAR_TEMP_D, 2
    scrollingmultichoice SCROLLINGMULTICHOICE_BOTANYEVOLUTIONCHAPTER, 0, 2, 5, 1
    compare VAR_RESULT, 127
    goto_if_eq RyuBotanyRecipeBookScript
    addvar VAR_RESULT, NUM_CONSUMABLE_RECIPES
    addvar VAR_RESULT, NUM_MEDICINE_RECIPES
    trycraftingrecipe VAR_RESULT
    goto RB_CheckCraftingResult

RB_CheckCraftingResult::
    clearflag  FLAG_RYU_DISPLAY_BOTANY_INGREDIENTS
    buffernumberstring 0, VAR_TEMP_C
    switch VAR_TEMP_C
    case 0, RB_RecipeFailed
    case 2000, RB_NotEnoughSpaceForResult
    case 4000, RB_MissingSomething
    case 8000, RB_NeedMoreOfSomething
    additem VAR_TEMP_C, 1
    bufferitemname 0, VAR_TEMP_C
    playfanfare MUS_FANFA1
    msgbox RB_Success
    release
    end

RB_RecipeFailed::
    msgbox RB_Error
    closemessage
    release
    end

RB_MissingSomething::
    msgbox RB_SeemToBeMissingSomething
    closemessage
    release
    end

RB_NeedMoreOfSomething::
    msgbox RB_SeemToNeedMoreOfSomething
    closemessage
    release
    end

RB_NotEnoughSpaceForResult::
    msgbox RB_NeedMoreSpace
    closemessage
    release
    end

WhichTypeOfRecipe:
    .string "Would you like to do Alchemy\nor Botany?$"

RB_Error:
    .string "{COLOR LIGHT_RED}{SHADOW RED}There was an error crafting this recipe.$"

RB_NeedMoreSpace::
    .string "You don't have enough space for\nthe item you're trying to craft.$"

RB_SeemToBeMissingSomething:
    .string "{COLOR LIGHT_GREEN}{SHADOW GREEN} You seem to be missing something...$"

RB_SeemToNeedMoreOfSomething:
    .string "{COLOR LIGHT_GREEN}{SHADOW GREEN}The mixture appears to be right,\nbut you seem to need more of\lsomething...$"

RB_WhichChapter:
    .string "Open which chapter?$"

RB_Success:
    .string "You successfully crafted one\n{STR_VAR_1}!$"

RB_YouSelected:
    .string "You selected option number {STR_VAR_1}.$"

RB_WhichRecipe:
    .string "Which recipe would you like to\ncraft?$"

RyuBotany_Obtained1Item:
    .string "You obtained a {STR_VAR_1}.$"

RyuBotany_Obtained2Items:
    .string "You obtained {STR_VAR_1} and {STR_VAR_2}.$"

RyuBotany_Obtained3Items:
    .string "You obtained these items:\n{STR_VAR_1}, {STR_VAR_2}, and {STR_VAR_3}.$"

RyuBotany_Obtained4Items:
    .string "You obtained these items:\n{STR_VAR_1}, {STR_VAR_2},\n{STR_VAR_3}, and {RYU_STR_1}.$"

RyuBotany_Obtained5Items:
    .string "You obtained the following items:\p{STR_VAR_1}, {STR_VAR_2}, {STR_VAR_3},\n {RYU_STR_1}, {RYU_STR_2}.$"


@Apricorn ball maker

RyuBotany_ApricornBallCrafter::  @Add this script to the npc you want to craft balls out of apricorns
    call_if_unset FLAG_TEMP_3, RB_ApricornTutorialMsgs
    checkmoney 1000
    compare VAR_RESULT, FALSE
    goto_if_eq RB_CantAfford
    msgbox RB_RefineWhichApricorns
    callnative RyuBufferNumApricornsForMenu
    multichoicedefault 0, 0, MULTI_BOTANY_APRICORN_MENU, 7, 0
    switch VAR_RESULT
    case 0, RB_RefineRedApcn
    case 1, RB_RefineBlueApcn
    case 2, RB_RefineYellowApcn
    case 3, RB_RefineGreenApcn
    case 4, RB_RefinePinkApcn
    case 5, RB_RefineWhiteApcn
    case 6, RB_RefineBlackApcn
    goto RB_CancelRefineApcn

RB_RefineWhichApricorns:
    .string "Which apricorn type would you\nlike to refine?$"

RB_ApricornTutorialMsgs::
    msgbox RB_SeeApricornMenuQuery, MSGBOX_YESNO
    compare VAR_RESULT, YES
    call_if_eq RB_BallMenu
    msgbox RB_RefineApricornIntro
    setflag FLAG_TEMP_3
    closemessage
    return

RB_BallMenu::
    drawinfobox INFOBOXAPCNBALLMENU
    msgbox RB_PressBToReturn
    setflag FLAG_TEMP_3
    closemessage
    removeinfobox
    return

RB_RefineRedApcn::
    checkitem ITEM_RED_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_RED_APRICORN, 2
    additem ITEM_LEVEL_BALL, 1
    bufferitemname 0, ITEM_LEVEL_BALL
    goto RB_FinishRefining

RB_RefineBlueApcn::
    checkitem ITEM_BLUE_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_BLUE_APRICORN, 2
    additem ITEM_LURE_BALL, 1
    bufferitemname 0, ITEM_LURE_BALL
    goto RB_FinishRefining

RB_RefineYellowApcn::
    checkitem ITEM_YELLOW_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_YELLOW_APRICORN, 2
    additem ITEM_MOON_BALL, 1
    bufferitemname 0, ITEM_MOON_BALL
    goto RB_FinishRefining

RB_RefineGreenApcn::
    checkitem ITEM_GREEN_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_GREEN_APRICORN, 2
    additem ITEM_FRIEND_BALL, 1
    bufferitemname 0, ITEM_FRIEND_BALL
    goto RB_FinishRefining

RB_RefinePinkApcn::
    checkitem ITEM_PINK_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_PINK_APRICORN, 2
    additem ITEM_LOVE_BALL, 1
    bufferitemname 0, ITEM_LOVE_BALL
    goto RB_FinishRefining

RB_RefineWhiteApcn::
    checkitem ITEM_WHITE_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_WHITE_APRICORN, 2
    additem ITEM_FAST_BALL, 1
    bufferitemname 0, ITEM_FAST_BALL
    goto RB_FinishRefining

RB_RefineBlackApcn::
    checkitem ITEM_BLACK_APRICORN, 2
    compare VAR_RESULT, TRUE
    goto_if_ne RB_NotEnoughApricorns
    removeitem ITEM_BLACK_APRICORN, 2
    additem ITEM_HEAVY_BALL, 1
    bufferitemname 0, ITEM_HEAVY_BALL
    goto RB_FinishRefining

RB_FinishRefining::
    removemoney 1000
    msgbox RB_ThanksForRefining
    closemessage
    release
    end

RB_NotEnoughApricorns::
    msgbox RB_NotEnoughApcnMsg
    closemessage
    release
    end

RB_CantAfford::
    msgbox RB_CantAffordMsg
    closemessage
    release
    end

RB_CancelRefineApcn::
    msgbox RB_ComeAgain
    closemessage
    release
    end

RB_RefineApricornIntro:
    .string "Welcome! I'm Kurt.\pI'll turn your harvested apricorns\ninto balls you can use on\lyour journey for ¥1000.$"
    
RB_WhichApricorn:
    .string "Which apricorns should I use?$"

RB_CantAffordMsg:
    .string "Time is money, kid.\pI need money for my time!$"

RB_NotEnoughApcnMsg:
    .string "I need at least two of a kind\nof apricorn to create a ball.$"

RB_ThanksForRefining:
    .string "Here's your {STR_VAR_1}.\nThanks for your business!$"

RB_PressBToReturn:
    .string "Here's what balls I can make.\p {COLOR LIGHT_GREEN}{SHADOW GREEN}(Press B to return)$"

RB_ComeAgain:
    .string "Return when you want me to make\na ball for you!$"

RB_SeeApricornMenuQuery:
    .string "Would you like to see the\nlist of balls that can be made?$"
`

//Botany levelling

script RyuBotany_CheckLevelUp {
    switch(var(VAR_RYU_PLAYER_BOTANY_SKILL)) {
        case 0:
            return //player doesn't have the skill.
        case 5:
            return//player is already max skill.
        case 4:
            if (var(VAR_RYU_PLAYER_BOTANY_SKILL_EXP) >= 800) {
                addvar(VAR_RYU_PLAYER_BOTANY_SKILL, 1)
                buffernumberstring(0, VAR_RYU_PLAYER_BOTANY_SKILL)
                msgbox(RyuBotany_SkillLeveledUp)
                return
            }
            else{
                return
            }
        case 3:
            if (var(VAR_RYU_PLAYER_BOTANY_SKILL_EXP) >= 390){
                addvar(VAR_RYU_PLAYER_BOTANY_SKILL, 1)
                buffernumberstring(0, VAR_RYU_PLAYER_BOTANY_SKILL)
                msgbox(RyuBotany_SkillLeveledUp)
                return
            }
            else {
                return
            }
        case 2:
            if (var(VAR_RYU_PLAYER_BOTANY_SKILL_EXP) >= 150){
                addvar(VAR_RYU_PLAYER_BOTANY_SKILL, 1)
                buffernumberstring(0, VAR_RYU_PLAYER_BOTANY_SKILL)
                msgbox(RyuBotany_SkillLeveledUp)
                return
            }
            else {
                return
            }
        case 1:
            if (var(VAR_RYU_PLAYER_BOTANY_SKILL_EXP) >= 50){
                addvar(VAR_RYU_PLAYER_BOTANY_SKILL, 1)
                buffernumberstring(0, VAR_RYU_PLAYER_BOTANY_SKILL)
                msgbox(RyuBotany_SkillLeveledUp)
                return
            }
            else {
                return
            }
    }
    return
}

text RyuBotany_SkillLeveledUp {
    "You have increased your Botany skill.\nYour Botany skill is now lv.{STR_VAR_1}."
}   

script RyuAlchemy_ChangeHarvestLevel {
    msgbox("Choose the level you'd like to mine at.")
    numberentry(0, 9, 1)
    if (var(VAR_RESULT) > 3){
        setvar(VAR_RESULT, 3)
    }
    elif (var(VAR_RESULT) == 0){
        goto(RyuAlchemy_HarvestChangeFailed2)
    }
    copyvar(VAR_0x8002, VAR_RESULT)
    compare_var_to_var(VAR_0x8002, VAR_RYU_PLAYER_MINING_SKILL)
    goto_if_gt(RyuAlchemy_HarvestChangeFailed)
    copyvar(VAR_RYU_MINING_HARVEST_LV, VAR_0x8002)
    closemessage
    buffernumberstring(0, VAR_RYU_MINING_HARVEST_LV)
    msgbox("Harvest level has been set to {STR_VAR_1}.")
    release
    end
}

script RyuAlchemy_HarvestChangeFailed{
    msgbox(format("The value you have chosen exceeds your maximum level. You cannot use this value."))
    closemessage
    goto(RyuAlchemy_ChangeHarvestLevel)
}

script RyuAlchemy_HarvestChangeFailed2{
    msgbox(format("Zero is not a valid harvest level. Choose another level."))
    closemessage
    goto(RyuAlchemy_ChangeHarvestLevel)
}