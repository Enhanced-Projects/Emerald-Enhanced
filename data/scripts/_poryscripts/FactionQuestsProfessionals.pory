//professionals quests


script(global) RyuProfessionalsDailyQuest  {
    lock
    faceplayer
    checkfactionstanding(FACTION_PROFESSIONALS, 175)

    if ((var(VAR_RESULT) == TRUE) && (flag(FLAG_RYU_PROF_SPECIAL_FINISHED) == FALSE)) {
        goto(RyuProfessionalsFactionSpecialQuest)
    }

    if (flag(FLAG_FINISHED_PROFESSIONAL_DAILY) == TRUE) { //quest done
        msgbox("I'm currently working,\nPlease come back later.")
        release
        end
    }

    checkfactionstanding(FACTION_PROFESSIONALS, 200)

    if ((var(VAR_RESULT) == TRUE) && (var(VAR_RYU_SUCCESSFUL_DELIVERIES) > 60)) { //do professionals final quest
        msgbox("Heya Union Rep! All things\non this front appear to be\lgoing smoothly!")
        closemessage
    }

    if (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_PROFESSIONALS) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) { //Also put the new quest conclusion here. This is where it checks if the quest is complete. If so, sends to the common reward script.
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonProfessionalsRewardScript)
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("Ahh, {PLAYER}!\nHave you brought the {STR_VAR_1} {STR_VAR_2} yet?")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                release
                end
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Oh! It was a {STR_VAR_1}! Well it's not suitable for my work, so you can have it."))
                    goto(CommonProfessionalsRewardScript)
                    release
                    end
                }
                else {
                    msgbox(format("Hurry and hatch that egg, already!"))
                    release
                    end
                }
        }

    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_PROFESSIONALS) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_PROFESSIONALS)
        callnative(RyuSummonOriginalNPCscript)
        end
    }

    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_PROFESSIONALS)
    random(4)//increase this by the number of quests you add.
    if (flag(FLAG_RYU_VERBOSE_MODE) == TRUE) {
        call(RyuDoDailyQuestVerbosePrint)
    }
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_SODA_POP)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 12)
            msgbox(ProfessionalsQuestDesc1)
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_HYPER_POTION)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 2)
            msgbox("I'm too busy to travel to a\nPokemon Center.\pDo you happen to have a couple\nHyper Potions?\pI'll be able to continue my work.")
        case 2:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(format("I found this egg while working in the field. Can you hatch it so we can see what it is?"))
            closemessage
            msgbox(FactionsReceivedEggMsg)
        case 3:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_SMOOTH_ROCK)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 12)
            msgbox(format("I need some smooth sand to polish and sharpen my tools with, could you bring me one? Thanks."))
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    closemessage
    call_if_unset(FLAG_RYU_FACTION_TUTORIAL_UNLOCKED, RyuScript_FactionsTutorialQuery)
    release
    end
}

script CommonProfessionalsRewardScript {
    msgbox(format("Everything you do for the Union is noted!"))
    setflag(FLAG_FINISHED_PROFESSIONAL_DAILY)
    changefactionstanding(FACTION_PROFESSIONALS, 25, FALSE)
    random(4500)
    addvar(VAR_RESULT, 2000)
    buffernumberstring(0, VAR_RESULT)
    addmoney(VAR_RESULT)
    goto(CommonDailyQuestEnd)
}

script RyuProfessionalsFactionSpecialQuest {
    setflag(FLAG_RYU_PROF_SPECIAL_QUEST_AVAILABLE)
    msgbox("The union president has let us\nknow that she's looking for\psomeone to perform a special\ntask.\pPlease go meet our union\npresident!\pYou can find her at Adrasti\nWarehouse in Mauville!")
    release
    end
}

//strings
text ProfessionalsQuestDesc1 {
    "We're running low on energy.\p"
    "Can you bring us some Soda Pops?\p"
    "We're in a crunch to meet\n"
    "some deadlines, the caffeine will\l"
    "help.\p"
    "Twelve of them should be enough."
}

text(global) FactionsReceivedEggMsg {
    "{PLAYER} received an Egg!"
}
