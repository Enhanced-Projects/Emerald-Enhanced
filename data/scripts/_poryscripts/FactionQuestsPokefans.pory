//Pokefans stuff


script(global) RyuPokefansDailyQuest  {
     if (flag(FLAG_FINISHED_POKEFAN_DAILY) == TRUE) { //quest done
        msgbox("We're all happy with what we\nhave at the moment, thanks!")
        release
        end
    }

    if (flag(FLAG_RYU_POKEFANS_FINAL_QUEST_IN_PROGRESS) == TRUE) {
        msgbox("I heard you're working for the\nPresident now!\pThat's so cool.")
        closemessage
        release
        end
    }

    if ((flag(FLAG_RYU_POKEFANS_FINAL_QUEST_STARTED) == TRUE) && (flag(FLAG_RYU_POKEFANS_FINAL_QUEST_DONE) == FALSE)) {
        msgbox("You really need to go meet\nthe President!\pWhere is he?\pWhy, the Lilycove city Fanclub,\nof course!")
        closemessage
        release
        end
    } 

    checkfactionstanding(FACTION_POKEFANS, 175)

    if (var(VAR_RESULT) == TRUE) {
        msgbox("Hey, {PLAYER}? have you met the fanclub\nPresident?\pHe reached out and let us know\nthat he'd like to mee you!\pyou can find him in the fanclub\lin Lilycove!\pPlease go meet him!")
        setflag(FLAG_RYU_POKEFANS_FINAL_QUEST_STARTED)
        release
        end
    }

    if (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_POKEFANS) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) { //Also put the new quest conclusion here. This is where it checks if the quest is complete. If so, sends to the common reward script.
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonPokefansRewardScript)
                    release
                    end
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("Hello, dear, we're still trying\nto get {STR_VAR_1} {STR_VAR_2}.")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                release
                end
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Oh! So it was a {STR_VAR_1}! It's so cute! Please take care of this Pokemon for us!"))
                    goto(CommonPokefansRewardScript)
                    release
                    end
                }
                else {
                    msgbox(format("Have you hatched that egg yet? I want to see what it is!"))
                    release
                    end
                }
        }
    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_POKEFANS) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_POKEFANS)
        callnative(RyuSummonOriginalNPCscript)
        end
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_POKEFANS)
    random(3)//increase this by the number of quests you add.
    if (flag(FLAG_RYU_VERBOSE_MODE) == TRUE) {
        call(RyuDoDailyQuestVerbosePrint)
    }
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_POKE_DOLL)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 5)
            msgbox("Hello, there!\pWe're trying to find some dolls\nto give to our kids.\pPlease find us 5 Poke Dolls.")
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_HEART_SCALE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 5)
            msgbox(PokefansQuest2Desc)
        case 2:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(PokefansQuest3Desc)
    }
    closemessage
    call_if_unset(FLAG_RYU_FACTION_TUTORIAL_UNLOCKED, RyuScript_FactionsTutorialQuery)
    release
    end
}

script CommonPokefansRewardScript {
    msgbox("Thanks, {PLAYER}.\nWe really appreciate your work!")
    setflag(FLAG_FINISHED_POKEFAN_DAILY)
    changefactionstanding(FACTION_POKEFANS, 25, FALSE)
    random(2500)
    addvar(VAR_RESULT, 3000)
    buffernumberstring(0, VAR_RESULT)
    addmoney(VAR_RESULT)
    goto(CommonDailyQuestEnd)
}



//strings

text PokefansQuest2Desc {
    "Hi, {PLAYER}!\n"
    "Is there a small chance you could\l"
    "find us five Heart Scales?\p"
    "They're beautiful collector's items\n"
    "and we want to reward them for\l"
    "a talent contest!"
}

text PokefansQuest3Desc {
    "Hi, {PLAYER}!\n"
    "We recently received an Egg that was\l"
    "anonymously dropped off.\p"
    "Is there any chance you could hatch\n"
    "it for us?"
}

//Pokefans special content
//pretty generic and lazy, i know, but hey, it could be expanded upon later

script RyuPokefansEscortMission { //generate an npc and location tos end player with and to
    if (flag(FLAG_RYU_POKEFANS_ESCORT_DONE) == TRUE){
        additem(VAR_RYU_POKEFANS_REWID, 1)
        msgbox("Thank you so much for your help!\nHere's your payment.")
        random(100)
        if (var(VAR_RESULT) < 5){
            additem(VAR_RYU_POKEFANS_REWID, 1)
            msgbox("Oh! There were two tokens\nstuck together!\lHow lucky you are!")
            closemessage
        }
        closemessage
        bufferitemname(0, VAR_RYU_POKEFANS_REWID)
        msgbox(" {COLOR DARK_GREY}{SHADOW LIGHT_GREY}(You received a {STR_VAR_1})")
        clearflag(FLAG_RYU_POKEFANS_ESCORT_DONE)
        closemessage
        release
        end
    }
    random(10)
    copyvar(VAR_RYU_POKEFANS_REWID, VAR_RESULT)
    callnative(RyuRandomizeEscortReward)
    msgbox("Ah! {PLAYER}!\pI have a task for you!\nI'd like you to escort someone.")
    closemessage
    callnative(RyuSetupPokefansEscortQuest)
    copyvar(VAR_OBJ_GFX_ID_8, VAR_RYU_POKEFANS_OBJID)
    addobject(12)
    applymovement(12, BringEscorteeOnScreen)
    waitmovement(12)
    msgbox("This person would like to go\nto {STR_VAR_1}.\pThanks ahead of time!")
    removeobject(12)
    createfollower(VAR_RYU_POKEFANS_OBJID, RyuPokefansEscortFollowerScript, DIR_EAST)
    release
    end
}

script RyuPokefansEscortFollowerScript {
    callnative(RyuBufferPokefansTarget)
    msgbox("I'm going to {STR_VAR_1}.\nThanks for your help!")
    closemessage
    release
    end
}