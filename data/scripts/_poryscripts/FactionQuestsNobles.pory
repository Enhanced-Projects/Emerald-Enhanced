//quests for Nobles

//Procedural components
const FETCH_TYPE = 1
const CAPTURE_TYPE = 2
const TRAVEL_TYPE = 3
const HATCH_TYPE = 4


script(global) RyuNoblesDailyQuest  {
    lock
    faceplayer
    checkfactionstanding(FACTION_NOBLES, 140)
    if ((var(VAR_RESULT) == TRUE) && (flag(FLAG_RYU_COMPLETED_NOBLES_SPECIAL_QUEST) == FALSE)) {
        goto(RyuNoblesFactionSpecialQuest)
    }

    if (flag(FLAG_FINISHED_NOBLE_DAILY) == TRUE) { //quest done
        msgbox("There are no outstanding contracts.")
        release
        end
    }

    checkfactionstanding(FACTION_NOBLES, 175)

    if ((var(VAR_RESULT) == TRUE) && (flag(FLAG_RYU_COMPLETED_NOBLES_FINAL_QUEST) == FALSE)) {
        goto(RyuNoblesFactionFinalQuest)
        msgbox("The Board is pleased with your\nperformance. One day, they may\linclude you in their businesses.")
        release
        end
    }

    if (flag(FLAG_RYU_NOBLES_FINAL_QUEST_IN_PROGRESS) == TRUE) {
        msgbox("You're still working for the\nchairperson.\pI might have some tasks for you\nwhen you have concluded your business\lwith him.")
        release
        end
    }

    if (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_NOBLES) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) { //Also put the new quest conclusion here. This is where it checks if the quest is complete. If so, sends to the common reward script.
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonNobleRewardScript)
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("The Board is still looking to\nacquire {STR_VAR_1} {STR_VAR_2}.")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                if(var(VAR_RYU_DAILY_QUEST_DATA) == 1) {
                    msgbox(format("So that's what they're up to. All right, you are dismissed."))
                    goto(CommonNobleRewardScript)
                }
                else {
                    buffermapname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Why haven't you been to {STR_VAR_1} yet? We already ordered you to go there. Hop to it!"))
                    release
                    end
                }
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Oh... It was a {STR_VAR_1}... Well... you can have it."))
                    goto(CommonNobleRewardScript)
                    release
                    end
                }
                else
                {
                    msgbox(format("Did a noble PokÃ©mon come out of that egg yet?"))
                    release
                    end
                }
        }

    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_NOBLES) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_NOBLES)
        callnative(RyuSummonOriginalNPCscript)
        end
    }

    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_NOBLES)
    random(3)//increase this by the number of quests you add. 
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_TEA)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 5)
            msgbox("The board of Directors would like\nsome Tea for their upcoming meeting.\pPlease acquire five servings.")
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_BIG_PEARL)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 8)
            msgbox("The board is trying to expand it's\nportfolio of riches.\pPlease acquire some Big Pearls\nfor us to hold.\pWe need at least eight.")
        case 2:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, TRAVEL_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, MAP_RUSTBORO_CITY_DEVON_CORP_2F)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            msgbox(format("The board needs you to go see what Devon Corp is up to. Head there to the second floor and see what their latest project is. Stocks are on the line here!"))
        case 3:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(format("One of our shareholders left us this Egg. Can you hatch it to see if it has any potential?"))
            closemessage
            msgbox(FactionsReceivedEggMsg)
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    closemessage
    release
    end
}

script CommonNobleRewardScript {
    msgbox("The Board should be pleased.\nYou are dismissed.")
    setflag(FLAG_FINISHED_NOBLE_DAILY)
    changefactionstanding(FACTION_NOBLES, 25, FALSE)
    buffernumberstring(0, 10000)
    addmoney(10000)
    goto(CommonDailyQuestEnd)
}

script RyuNoblesFactionSpecialQuest {
    if (defeated(TRAINER_BAD_TENANT) == TRUE) {
        goto(RyuNoblesSpecialRewardScript)
    }
    msgbox("The Board is having issues with\ngetting a certain tenant to vacate\la premises.\pCould you go and...\nassist with the eviction?\pThe house in question is in Lilycove.")
    closemessage
    msgbox("He's been causing all kinds of\ntrouble for the citizens on the beach\land swimming in the cove.\pWe've received multiple complaints.\pHe has to go.")
    setflag(NOBLES_SPECIAL_MISSION_IN_PROGRESS)
    release
    end
}

script RyuNoblesSpecialFactionQuestTrigger { //This should go on a flor trigger in front of the door to the lilycove house.
    if(flag(NOBLES_SPECIAL_MISSION_IN_PROGRESS) == FALSE) {
        goto(ryu_end)
    }
    lock
    faceplayer
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (You hear someone inside shouting\n something about getting off their\l lawn.)")
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_StepDown)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    playse(SE_DOOR)
    addobject(69)//whatever the special NPC is for this quest
    msgbox("I already told you people.\nYou can't evict me, my rent is\ppaid and my maintenance is up to\ndate.\pYou can't just boot me out because\nyou don't like me.")
    closemessage
    msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}Actually....\pIt's right here in the lease.\pWe can evict anyone for any reason\nwe want.\pIf you were just a decent human\nbeing rather than shouting expletives\lat beach-goers...\pWe wouldn't be having this problem.")
    closemessage
    msgbox("Well, they're noisy.\pI like peace and quiet.\nThey should go swim somewhere else.")
    closemessage
    msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}That's not how that works.\pNot only do you not own the ocean\nor the beach, you don't even own\lthis house.\pYou have to go.\nI am here to facilitate that.\pGet a move on.")
    closemessage
    msgbox("I will NOT be forced out of this\nhouse by the likes of you.")
    closemessage
    cleartrainerflag(TRAINER_BAD_TENANT)
    trainerbattle_single(TRAINER_BAD_TENANT, TenantBattleStartText, TenantLoseText, NoblesQuestAfterBattleScript)
    release
    end
}

script NoblesQuestAfterBattleScript {
    msgbox("I see. I'll leave now.\nBut I'm taking this to court.")
    closemessage
    msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}That's fine.\nWe're within our rights.\lHave a nice day!")
    closemessage
    release
    end
}

script RyuNoblesSpecialRewardScript {
    msgbox("I see you were able to get the\nproblematic man to vacate the\lpremeses.\pIt seems like you have a knack for\nreal estate, but it's not for me\lto decide.\pHere's your pay for the job.\nExcellent work, Statesman.")
    addmoney(15000)
    setflag(FLAG_RYU_COMPLETED_NOBLES_SPECIAL_QUEST)
    addvar(VAR_RYU_QUESTS_FINISHED, 1)
    clearflag(NOBLES_SPECIAL_MISSION_IN_PROGRESS)
    release
    end
}

script RyuNoblesFactionFinalQuest {
    msgbox("The Board chairperson is looking\nto speak to you directly.\pYou should go find him at any\nbranch of Frontier Bank.")
    setflag(FLAG_RYU_NOBLES_FINAL_QUEST_IN_PROGRESS)
    release
    end
}

text TenantBattleStartText {
    "{COLOR LIGHT_BLUE}{SHADOW BLUE}We'll see about that."
}

text TenantLoseText {
    "So they went and hired a real\nthug, after all."
}

