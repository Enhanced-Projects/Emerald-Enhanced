const SHELLY = 8
const DAWN = 6
const BRENDAN = 9
const MAY = 13
const LANETTE = 5
const LEAF = 7
const JOY = 11
const MINNIE = 4
const COURTNEY = 14
const LUCY = 15
const MOM = 16

script Ryu_TestAdvancedHaremEvent {
	setflag(FLAG_RYU_DS_LUCY_PARTNERS)
	setflag(FLAG_RYU_DS_LANETTE_PARTNERS)
	setflag(FLAG_RYU_DS_LEAF_PARTNERS)
	setflag(FLAG_RYU_DS_DAWN_PARTNERS)
	setflag(FLAG_RYU_DS_MAY_PARTNERS)
	setflag(FLAG_RYU_DS_SHELLY_PARTNERS)
	setflag(FLAG_RYU_DS_COURTNEY_PARTNERS)
	setflag(FLAG_RYU_DS_JOY_PARTNERS)
	setflag(FLAG_RYU_COMPLETED_MAY_INTRO_WITH_DAWN)
	setflag(FLAG_RYU_COMPLETED_MAY_RT_SCENE_WITH_PARTNERS)
	setflag(FLAG_RYU_MINNIE_GAME_CLEAR)
	setflag(FLAG_RYU_MINNIE_POKEMON_SCHOOL)
	clearflag(FLAG_RYU_HAREM_EVENT_COMPLETED)
	clearflag(FLAG_RYU_HAREM_EVENT_STARTED)
	setvar(VAR_TEMP_D, 0)
}

script LRT_EliminateMom{
	msgbox("Don't mind me,\njust getting some air.")
	closemessage
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_MOM)
	applymovement(MOM, LRT_HE_MomToHouse)
	waitmovement(MOM)
	playse(SE_DOOR)
	removeobject(MOM)
	end
}

script Ryu_HaremEvent {
	setvar(VAR_TEMP_2, 100)
	if (var(VAR_RYU_PARTNER_COUNT) < 5){
		setflag(FLAG_RYU_HIDE_HAREM_EVENT_NPCS)
		goto(ryu_end)
	}
	if (flag(FLAG_RYU_HAREM_EVENT_COMPLETED) == TRUE){
		setflag(FLAG_RYU_HIDE_HAREM_EVENT_NPCS)
		goto(ryu_end)
	}
	if (flag(FLAG_RYU_HAREM_EVENT_STARTED) == FALSE){
		setflag(FLAG_RYU_HIDE_HAREM_EVENT_NPCS)
		goto(ryu_end)
	}
	clearflag(FLAG_RYU_HAREM_EVENT_STARTED)
	fadenewbgm(MUS_THANKFOR)
	addobject(MOM)
	call(RyuRivalHaremIntro)
	if (flag(FLAG_RYU_DS_SHELLY_PARTNERS) == TRUE) {
		addobject(SHELLY)
		call(RyuHaremEventShelly)
	}
	if (flag(FLAG_RYU_DS_JOY_PARTNERS) == TRUE) {
		addobject(JOY)
		call(RyuHaremEventJoy)
	}
	if (flag(FLAG_RYU_DS_LANETTE_PARTNERS) == TRUE) {
		addobject(LANETTE)
		call(RyuHaremEventLanette)
	}
	if (flag(FLAG_RYU_DS_LUCY_PARTNERS) == TRUE) {
		addobject(LUCY)
		call(RyuHaremEventLucy)
	}
	if (flag(FLAG_RYU_DS_LEAF_PARTNERS) == TRUE) {
		addobject(LEAF)
		call(RyuHaremEventLeaf)
	}
	if (flag(FLAG_RYU_DS_MAY_PARTNERS) == TRUE) {
		call(RyuHaremEventMay)
	}
	if ((flag(FLAG_RYU_MINNIE_GAME_CLEAR) == TRUE) && (flag(FLAG_RYU_MINNIE_POKEMON_SCHOOL) == TRUE)) {
		call(RyuHaremEventMinnie)
	}
	turnobject(PLAYER, DIR_SOUTH)
	turnobject(DAWN, DIR_SOUTH)
	turnobject(BRENDAN, DIR_SOUTH)
	if (flag(FLAG_RYU_DS_COURTNEY_PARTNERS) == TRUE) {
		addobject(COURTNEY)
		call(RyuHaremEventCourtney)
	}
	turnobject(PLAYER, DIR_EAST)
	turnobject(MOM, DIR_WEST)
	turnobject(DAWN, DIR_WEST)
	turnobject(BRENDAN, DIR_WEST)
	call(RyuHaremEventRivalOutro)
	goto(RyuEndHaremEvent)
}

//Rival introduces the harem clique
script RyuRivalHaremIntro {
	turnobject(PLAYER, DIR_EAST)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}They all asked me to bring you out here so that they could express their thanks to you.\pAs your mother, I'm delighted that you've made so many friends!"))
	closemessage
	turnobject(PLAYER, DIR_SOUTH)
	turnobject(DAWN, DIR_SOUTH)
	turnobject(BRENDAN, DIR_SOUTH)
	turnobject(MOM, DIR_SOUTH)
	return
}

//moves the current object to the left one space because the player is female.
script RyuHaremEventIntroMoveOverForFemalePlayer {
	checkplayergender
	if (var(VAR_RESULT) == FEMALE) {
		return
	}
	applymovement(VAR_TEMP_3, Common_Movement_StepLeft)
	waitmovement(VAR_TEMP_3)
	return
}

//player recruited shelly
script RyuHaremEventShelly {
	setvar(VAR_TEMP_3, SHELLY)
	applymovement(SHELLY, LRT_HE_PartnerMove)
	waitmovement(SHELLY)
	call(RyuHaremEventIntroMoveOverForFemalePlayer)
	turnobject(SHELLY, DIR_NORTH)
	applymovement(SHELLY, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You swept me off my feet like a strong wave..."))
	closemessage
	return
}

//player recruited courtney
script RyuHaremEventCourtney {
	setvar(VAR_TEMP_3, COURTNEY)
	applymovement(COURTNEY, LRT_HE_PartnerMove)
	waitmovement(COURTNEY)
	call(RyuHaremEventIntroMoveOverForFemalePlayer)
	turnobject(COURTNEY, DIR_NORTH)
	applymovement(COURTNEY, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You ignited my fiery passion..."))
	closemessage
	setflag(FLAG_RYU_HAREM_EVENT_COURTNEY_SPECIAL)
	return
}

//player recruited joy
script RyuHaremEventJoy {
	setvar(VAR_TEMP_3, JOY)
	call(RyuHaremEventIntroMoveOverForFemalePlayer)
	applymovement(JOY, LRT_HE_PartnerMove)
	waitmovement(JOY)
	turnobject(JOY, DIR_NORTH)
	applymovement(JOY, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You made me find a new joy in life..."))
	closemessage
	return
}

//player recruited lanette
script RyuHaremEventLanette {
	applymovement(LANETTE, LRT_HE_PartnerMoveLanette1)
	waitmovement(LANETTE)
	setvar(VAR_TEMP_3, LANETTE)
	call(RyuHaremEventIntroMoveOverForFemalePlayer)
	applymovement(LANETTE, LRT_HE_PartnerMoveLanette2)
	waitmovement(LANETTE)
	applymovement(LANETTE, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You pushed my expectations SO far out of the box..."))
	closemessage
	return
}

//player recruited Lucy
script RyuHaremEventLucy {
	applymovement(LUCY, LRT_HE_PartnerMoveLucy1)
	waitmovement(LUCY)
	applymovement(LUCY, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	waitmovement(LUCY)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You helped me shed evil from Hoenn's back..."))
	closemessage
	return
}

//player recruited leaf
script RyuHaremEventLeaf {
	setvar(VAR_TEMP_3, LEAF)
	applymovement(LEAF, LRT_HE_PartnerMoveLeaf1)
	waitmovement(LEAF)
	call(RyuHaremEventIntroMoveOverForFemalePlayer)
	applymovement(LEAF, LRT_HE_PartnerMoveLeaf2)
	waitmovement(LEAF)
	applymovement(LEAF, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You helped me find my voice again..."))
	closemessage
	return
}

//player recruited may
script RyuHaremEventMay {
	addobject(MAY)
	setvar(VAR_TEMP_3, MAY)
	call(RyuHaremEventIntroMoveOverForFemalePlayer)
	applymovement(MAY, LRT_HE_MayMove)
	waitmovement(MAY)
	turnobject(PLAYER, DIR_NORTH)
	turnobject(DAWN, DIR_NORTH)
	turnobject(BRENDAN, DIR_NORTH)
	applymovement(MAY, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	if ((flag(FLAG_RYU_COMPLETED_MAY_INTRO_WITH_DAWN) == TRUE) && (flag(FLAG_RYU_COMPLETED_MAY_RT_SCENE_WITH_PARTNERS) == TRUE)) {
		//Player completed May's optionals
		checkplayergender
		if (var(VAR_RESULT) == MALE) {
			msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You're such a stud... I totally get why they all like you so much!\pand also..."))
		}
		else {
			msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You're so sly... no small wonder they all fell for your trap!\pand also..."))
		}
		closemessage
	}
	msgbox(format("{COLOR LIGHT_RED}{SHADOW RED}You helped me find my purpose again!"))
	closemessage
	return
}

//player completed minnie's optionals
script RyuHaremEventMinnie {
	setvar(VAR_TEMP_3, MINNIE)
	applymovement(MAY, Common_Movement_StepRight)
	waitmovement(MAY)
	turnobject(MAY, DIR_SOUTH)
	applymovement(MINNIE, LRT_HE_MinnieMove)
	waitmovement(MINNIE)
	checkplayergender
	if (var(VAR_RESULT) == FEMALE) {
		applymovement(MINNIE, Common_Movement_StepRight)
		waitmovement(MINNIE)
		turnobject(MINNIE, DIR_SOUTH)
	}
	turnobject(PLAYER, DIR_NORTH)
	turnobject(DAWN, DIR_NORTH)
	turnobject(BRENDAN, DIR_NORTH)
	applymovement(MINNIE, Common_Movement_HeartEmote)
	playse(SE_M_CHARM)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}You're so popular!\pI hope I can stand next to you\nlike they do some day!")
	closemessage
	turnobject(PLAYER, DIR_SOUTH)
	turnobject(DAWN, DIR_SOUTH)
	turnobject(BRENDAN, DIR_SOUTH)
	return
}

script RyuHaremEventRivalOutro {//finish harem event
	turnobject(PLAYER, DIR_EAST)
	turnobject(DAWN, DIR_SOUTH)
	turnobject(BRENDAN, DIR_SOUTH)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}I'm so very proud of you,\nbut they actually got together\l to get you this.\pIt's a Shiny Charm!\nI hear that it's incredibly rare.\pLook at you go!\nWell that's all, honey.\pHave fun with your adventures!")
	closemessage
	clearflag(FLAG_RYU_HAREM_EVENT_STARTED)
	setflag(FLAG_RYU_HAREM_EVENT_COMPLETED)
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_DAWN)
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_BRENDAN)
	applymovement(MOM, LRT_HE_MomToHouse)
	waitmovement(MOM)
	playse(SE_DOOR)
	removeobject(MOM)
	return
}

script RyuHaremEventOutroMoveOverForFemalePlayer {
	checkplayergender
	if (var(VAR_RESULT) == FEMALE) {
		return
	}
	applymovement(VAR_TEMP_3, Common_Movement_StepRight)
	waitmovement(VAR_TEMP_3)
	turnobject(VAR_TEMP_3, DIR_NORTH)
	return
}

script RyuEndHaremEvent { //move objects offscreen and disappear them
	giveitem(ITEM_SHINY_CHARM, 1)
	waitfanfare
	closemessage
	checkplayergender
	if (var(VAR_RESULT) == FEMALE) {
		applymovement(LANETTE, Common_Movement_StepLeft)
		waitmovement(LANETTE)
	}
	applymovement(LANETTE, LRT_HE_LanetteExit)
	delay(30)
	applymovement(LEAF, LRT_HE_LeafExit)
	delay(30)
	applymovement(JOY, LRT_HE_JoyExit)
	delay(35)
	applymovement(COURTNEY, LRT_HE_CourtneyExit)
	delay(30)
	applymovement(SHELLY, LRT_HE_ShellyExit)
	delay(30)
	applymovement(MINNIE, LRT_HE_MinnieReturnToPost)
	checkplayergender
	applymovement(DAWN, LRT_HE_RivalExit)
	applymovement(BRENDAN, LRT_HE_RivalExit)
	delay(30)
	applymovement(MAY, LRT_HE_MayExit)
	delay(20)
	applymovement(LUCY, LRT_HE_LucyExit)
	waitmovement(LUCY)
	setvar(VAR_RYU_PARTNER_COUNT, 10)
	setflag(FLAG_RYU_HAREM_EVENT_COMPLETED)
	removeobject(DAWN)
	removeobject(BRENDAN)
	removeobject(SHELLY)
	removeobject(MAY)
	removeobject(JOY)
	removeobject(LANETTE)
	removeobject(LEAF)
	removeobject(COURTNEY)
	removeobject(LUCY)
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_NPCS)
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_BRENDAN)
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_DAWN)
	setflag(FLAG_RYU_HIDE_HAREM_EVENT_MOM)
	clearflag(FLAG_RYU_HAREM_EVENT_STARTED)
	call(RyuCheckForAdvancedEnd)
	fadenewbgm(MUS_TETSUJI)
	release
	end
}
//advanced harem end

script RyuCheckForAdvancedEnd{
	goto_if_unset(FLAG_RYU_COMPLETED_MAY_INTRO_WITH_DAWN, ryu_return)
	goto_if_unset(FLAG_RYU_COMPLETED_MAY_RT_SCENE_WITH_PARTNERS, ryu_return)
	goto_if_unset(FLAG_RYU_MINNIE_GAME_CLEAR, ryu_return)
	goto_if_unset(FLAG_RYU_MINNIE_POKEMON_SCHOOL, ryu_return)
	goto_if_unset(FLAG_RYU_DS_MAY_PARTNERS, ryu_return)
	goto_if_unset(FLAG_RYU_DS_LUCY_PARTNERS, ryu_return)
	goto_if_unset(FLAG_RYU_DS_LANETTE_PARTNERS, ryu_return)
	goto_if_unset(FLAG_RYU_DS_LEAF_PARTNERS, ryu_return)
	goto_if_unset(FLAG_RYU_DS_MAY_PARTNERS, ryu_return)
	goto_if_unset(FLAG_RYU_DS_JOY_PARTNERS, ryu_return)
	if (flag(FLAG_RYU_DS_SHELLY_PARTNERS) == FALSE){
		if (flag(FLAG_RYU_DS_COURTNEY_PARTNERS) == FALSE){
			goto(ryu_return)
		}
	}
	if (flag(FLAG_RYU_DS_DAWN_PARTNERS) == FALSE){
		if (flag(FLAG_RYU_DS_BRENDAN_PARTNERS) == FALSE){
			goto(ryu_return)
		}
	}
	if (var(VAR_RYU_PARTNER_COUNT) >= 7) {
		turnobject(PLAYER, DIR_SOUTH)
		msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}Huh. I really have done it\nall, haven't I?\p {COLOR LIGHT_GREEN}{SHADOW GREEN}(You take a second to reflect\n on yourself and your travels\l and you feel accomplished.)")
		ach(give, ACH_ULTIMATE_END)
		ach(give, ACH_TRUE_ENDING)
	}
	return
}
//movements

movement LRT_HE_LucyExit {
	walk_up
	walk_left
	walk_up * 6
	walk_right
	face_up
	step_end
}

movement LRT_HE_PartnerMoveLucy1 {
	walk_up * 4
	walk_left * 2
	face_up
	step_end
}

movement LRT_HE_MomToHouse {
	walk_right * 3
	walk_up
	walk_in_place_up
	step_end
}

movement LRT_HE_JoyExit {
	walk_right * 3
	walk_down * 5
	step_end
}

movement LRT_HE_RivalExit {
	walk_right
	walk_down * 7
	step_end
}

movement LRT_HE_MayExit {
	walk_up * 6
	step_end
}

movement LRT_HE_MinnieReturnToPost {
	walk_left * 2
	walk_up * 5
	step_end
}

movement LRT_HE_LanetteExit {
	walk_up * 8
	step_end
}

movement LRT_HE_LeafExit {
	walk_left * 2
	walk_up * 8
	step_end
}

movement LRT_HE_ShellyExit {
	walk_left * 9
	walk_down * 2
	step_end
}

movement LRT_HE_PartnerMove {
	walk_up * 4
	walk_left
	step_end
}

movement LRT_HE_PartnerMoveLeaf1 {
	walk_up * 3
	walk_left
	step_end
}

movement LRT_HE_PartnerMoveLeaf2 {
	walk_up
	step_end
}

movement LRT_HE_PartnerMoveLanette1 {
	walk_up * 3
	step_end
}

movement LRT_HE_PartnerMoveLanette2 {
	walk_left
	walk_up
	step_end
}

movement LRT_HE_MayMove {
	walk_left
	walk_down * 5
	step_end
}

movement LRT_HE_CourtneyExit {
	walk_up * 9
	step_end
}

movement LRT_HE_MinnieMove {
	walk_right * 2
	walk_down * 5
	face_down
	step_end
}
//=============================================May's part of the harem event

script RyuHaremEventMaySection {
	if ((flag(FLAG_RYU_COMPLETED_MAY_INTRO_WITH_DAWN) == TRUE) && (flag(FLAG_RYU_COMPLETED_MAY_RT_SCENE_WITH_PARTNERS) == TRUE)) {
		addobject(13)
		applymovement(13, LRT_HaremEvent_MaySectionMovement1)
		waitmovement(13)
		msgbox("{COLOR LIGHT_RED}{SHADOW RED}Hoo boy, you sure are popular with\nthe ladies, huh!?\pWell, I'm okay with all of them\nas long as they don't try to\lkeep you from me.\pI wish I could have participated\nin getting your gift, but, I came around\ltoo late. It's fine!\pI just have to be extra loving\nwhen I'm travelling with you, huh?")
		closemessage
		getplayerxy(VAR_0x8008, VAR_0x8009)
		if (var(VAR_0x8008) == 9) { //player is female, X coord is 9
			applymovement(13, Common_Movement_StepLeft)
			waitmovement(13)
		}
		turnobject(13, DIR_WEST)
		applymovement(13, Common_Movement_HeartEmote)
		playse(SE_M_CHARM)
		waitse
		msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (May blushes and hesitates for a little\n and then kisses you.)")
		playse(SE_PIN)
		applymovement(PLAYER, Common_Movement_ExclamationMark)
		waitmovement(PLAYER)
		waitse
		waitmessage
		msgbox("{COLOR LIGHT_RED}{SHADOW RED}There's my gift for you.\nHope you enjoyed it!")
		closemessage
		applymovement(13, LRT_HaremEvent_MayMovement2)
		waitmovement(13)
		removeobject(13)
		return
	}
	return
}

movement LRT_HaremEvent_MaySectionMovement1 {
	walk_down * 6
	face_left
	step_end
}

movement LRT_HaremEvent_MayMovement2 {
	walk_up * 6
	step_end
}
