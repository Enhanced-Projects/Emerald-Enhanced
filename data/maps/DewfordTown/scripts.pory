raw `
DewfordTown_MapScripts:: @ 81E9507
	map_script ON_TRANSITION, DewfordTown_OnTransition
	map_script ON_LOAD, DT_CheckCloseCave
	map_script ON_FRAME, DT_OnFrameScripts
	.byte 0

DewfordTown_OnTransition: @ 81E950D
	setflag FLAG_VISITED_DEWFORD_TOWN
	call RyuCheckForWellTravelledAch
	end

DT_OnFrameScripts::
	map_script_2 VAR_RYU_QUEST_GENESECT, 50, DT_DFT_ReturnFromPast
	.2byte 0

DT_CheckCloseCave::
	compare VAR_RYU_QUEST_GENESECT, 50
	call_if_lt DT_CloseCave
	compare VAR_RYU_QUEST_GENESECT, 15
	call_if_eq DT_ShowSumac
	end

DT_CloseCave::
	setmetatile 11, 10, METATILE_General_RockWall_SandBase, 1
	setmetatile 11, 11, METATILE_General_RockWall_SandBase, 1
	return

DT_ShowSumac::
	clearflag FLAG_RYU_DFT_BARD_GENESECT
	addobject 5
	return

DewfordTown_EventScript_Briney:: @ 81E9511
	goto_if_set FLAG_RYU_DT_BRINEY_BATTLE, ryu_end @@This prevents briney from approaching player if he's been defeated, i.e. not hostile.
	lockall
	playse SE_PIN
	applymovement 10, Common_Movement_ExclamationMark
	waitmovement 10
	delay 30
	getplayerxy VAR_TEMP_2, VAR_TEMP_3
	switch VAR_TEMP_2
	case 18, DewfordTown_EventScript_Briney_Move5Right
	case 17, DewfordTown_EventScript_Briney_Move4Right
	case 16, DewfordTown_EventScript_Briney_Move3Right
	case 15, DewfordTown_EventScript_Briney_Move2Right
	case 14, DewfordTown_EventScript_Briney_FaceRight
	case 12, DewfordTown_EventScript_Briney_FaceLeft
	goto DT_Briney_Battle


DewfordTown_EventScript_Briney_Move5Right::
	applymovement 10, DT_Briney_5Right
	waitmovement 10
	call DT_TurnPlayer
	applymovement PLAYER, Common_Movement_FaceRight
	goto DT_Briney_Battle

DewfordTown_EventScript_Briney_Move4Right::
	applymovement 10, DT_Briney_4Right
	waitmovement 10
	goto DT_Briney_Battle
	
DewfordTown_EventScript_Briney_Move3Right::
	applymovement 10, DT_Briney_3Right
	waitmovement 10
	goto DT_Briney_Battle
	
DewfordTown_EventScript_Briney_Move2Right::
	applymovement 10, DT_Briney_2Right
	waitmovement 10
	goto DT_Briney_Battle
	
DewfordTown_EventScript_Briney_FaceRight::
	applymovement 10, Common_Movement_FaceRight
	waitmovement 10
	goto DT_Briney_Battle
	
DewfordTown_EventScript_Briney_FaceLeft::
	getplayerxy VAR_TEMP_2, VAR_TEMP_3
	compare VAR_TEMP_3, 110
	goto_if_ne DT_Briney_Battle
	applymovement 10, Common_Movement_FaceDown
	waitmovement 10
	goto DT_Briney_Battle

DT_Briney_Battle::
	setflag FLAG_TEMP_6
	setflag FLAG_RYU_DT_BRINEY_BATTLE
	trainerbattle_single TRAINER_BRINEY, DewfordTown_Text_BrineyIntro, DewfordTown_Text_BrineyDefeated
	release
	end
	
DT_Briney_PostBattle::
	msgbox DewfordTown_Text_BrineyPostBattle, MSGBOX_AUTOCLOSE
	release
	end

DT_Briney_PostBattleActvate::@@the script that activates when you talk directly to briney
	goto_if_unset FLAG_RYU_DT_BRINEY_BATTLE, DT_Briney_Battle                        @@Forces a fight with player when interacted
	msgbox DewfordTown_Text_BrineyPostBattle, MSGBOX_AUTOCLOSE                       @@with directly if not already defeated. 
	release                                                                         
	end

DT_TurnPlayer::
	getplayerxy VAR_TEMP_2, VAR_TEMP_3
	compare VAR_TEMP_3, 110
	goto_if_eq DT_TurnPlayerUp
	applymovement PLAYER, Common_Movement_FaceLeft
	waitmovement PLAYER
	return

DT_TurnPlayerUp::
	applymovement PLAYER, Common_Movement_FaceUp
	waitmovement PLAYER
	return

DT_Briney_5Right:
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	step_end

DT_Briney_4Right:
	walk_right
	walk_right
	walk_right
	walk_right
	step_end

DT_Briney_3Right:
	walk_right
	walk_right
	walk_right
	step_end

DT_Briney_2Right:
	walk_right
	walk_right
	step_end
	

DewfordTown_EventScript_Woman:: @ 81E95B1
	msgbox DewfordTown_Text_TinyIslandCommunity, MSGBOX_NPC
	end

DewfordTown_EventScript_TownSign:: @ 81E95BA
	msgbox DewfordTown_Text_TownSign, MSGBOX_SIGN
	end

DewfordTown_EventScript_GymSign:: @ 81E95C3
	msgbox DewfordTown_Text_GymSign, MSGBOX_SIGN
	end

DewfordTown_EventScript_HallSign:: @ 81E95CC
	msgbox DewfordTown_Text_HallSign, MSGBOX_SIGN
	end

DewfordTown_EventScript_OldRodFisherman:: @ 81E95D5
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_OLD_ROD, DewfordTown_EventScript_HowsFishing
	msgbox DewfordTown_Text_GettingItchToFish, MSGBOX_YESNO
	compare VAR_RESULT, YES
	goto_if_eq DewfordTown_EventScript_GiveOldRod
	compare VAR_RESULT, NO
	goto_if_eq DewfordTown_EventScript_NotGettingItchToFish
	end

DewfordTown_EventScript_GiveOldRod:: @ 81E95FF
	msgbox DewfordTown_Text_GiveYouOneOfMyRods, MSGBOX_DEFAULT
	giveitem ITEM_OLD_ROD
	setflag FLAG_RECEIVED_OLD_ROD
	msgbox DewfordTown_Text_ThrowInFishingAdvice, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_NotGettingItchToFish:: @ 81E9620
	msgbox DewfordTown_Text_ThatsTooBadThen, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_HowsFishing:: @ 81E962A
	message DewfordTown_Text_HowsYourFishing
	waitmessage
	multichoice 20, 8, MULTI_HOWS_FISHING, 1
	compare VAR_RESULT, 0
	goto_if_eq DewfordTown_EventScript_FishingExcellent
	compare VAR_RESULT, 1
	goto_if_eq DewfordTown_EventScript_FishingNotSoGood
	end

DewfordTown_EventScript_FishingExcellent:: @ 81E964C
	msgbox DewfordTown_Text_GreatHaulInSomeBigOnes, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_FishingNotSoGood:: @ 81E9656
	msgbox DewfordTown_Text_FishingAdvice, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_TrendyPhraseBoy:: @ 81E9922
	lock
	faceplayer
	call Common_EventScript_BufferTrendyPhrase
	msgbox DewfordTown_Text_XIsTheBiggestHappeningThingRight, MSGBOX_YESNO
	compare VAR_RESULT, YES
	goto_if_eq DewfordTown_EventScript_ConfirmTrendyPhrase
	compare VAR_RESULT, NO
	goto_if_eq DewfordTown_EventScript_RejectTrendyPhrase
	end

DewfordTown_EventScript_ConfirmTrendyPhrase:: @ 81E9948
	msgbox DewfordTown_Text_YeahDefinitionOfInRightNow, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_RejectTrendyPhrase:: @ 81E9952
	msgbox DewfordTown_Text_TellMeWhatsNewAndIn, MSGBOX_DEFAULT
	setvar VAR_0x8004, EASY_CHAT_TYPE_TRENDY_PHRASE
	call Common_ShowEasyChatScreen
	lock
	faceplayer
	compare VAR_RESULT, 1
	goto_if_eq DewfordTown_EventScript_GiveNewTrendyPhrase
	compare VAR_RESULT, 0
	goto_if_eq DewfordTown_EventScript_CancelNewTrendyPhrase
	end

DewfordTown_EventScript_GiveNewTrendyPhrase:: @ 81E997D
	incrementgamestat GAME_STAT_STARTED_TRENDS
	compare VAR_0x8004, 0
	goto_if_eq DewfordTown_EventScript_PhraseNotTrendyEnough
	msgbox DewfordTown_Text_OfCourseIKnowAboutThat, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_CancelNewTrendyPhrase:: @ 81E9994
	msgbox DewfordTown_Text_HearOfAnyTrendsComeShareWithMe, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_PhraseNotTrendyEnough:: @ 81E999E
	msgbox DewfordTown_Text_XHuhIThinkYIsCool, MSGBOX_DEFAULT
	release
	end

DewfordTown_Text_TinyIslandCommunity: @ 81E99A8
	.string "Dewford is a tiny island community.\n"
	.string "If something gets trendy here,\l"
	.string "everyone picks up on it right away.$"

DewfordTown_Text_TownSign: @ 81E9A0F
	.string "Dewford Town\n"
	.string "“A tiny island in the blue sea.”$"

DewfordTown_Text_GymSign: @ 81E9A3D
	.string "Dewford Town Pokémon Gym\n"
	.string "Leader: Brawly\l"
	.string "“A big wave in fighting!”$"

DewfordTown_Text_HallSign: @ 81E9A7F
	.string "Dewford Hall\n"
	.string "“Everyone's information exchange!”$"

DewfordTown_Text_GettingItchToFish: @ 81E9DD1
	.string "This is a renowned fishing spot.\n"
	.string "Are you getting the itch to fish?$"

DewfordTown_Text_GiveYouOneOfMyRods: @ 81E9E14
	.string "I hear you, and I like what\n"
	.string "you're saying!\p"
	.string "I'll give you one of my fishing Rods.$"

DewfordTown_Text_ThrowInFishingAdvice: @ 81E9E65
	.string "And, as an added bonus, I'll even throw\n"
	.string "in a little fishing advice!\p"
	.string "First, you want to face the water,\n"
	.string "then use the Rod.\p"
	.string "Focus your mind…\n"
	.string "If you get a bite, pull on the Rod.\p"
	.string "Sometimes you can snag something\n"
	.string "immediately, but with bigger catches,\l"
	.string "you need to time the pulls on your Rod\l"
	.string "to haul them in.$"

DewfordTown_Text_ThatsTooBadThen: @ 81E9F92
	.string "Oh, is that so?\n"
	.string "That's too bad, then.$"

DewfordTown_Text_HowsYourFishing: @ 81E9FB8
	.string "Yo!\n"
	.string "How's your fishing?$"

DewfordTown_Text_GreatHaulInSomeBigOnes: @ 81E9FD0
	.string "Is that right! That's great!\n"
	.string "Haul in some big ones!$"

DewfordTown_Text_FishingAdvice: @ 81EA004
	.string "Oh, hey, don't get down on yourself!\n"
	.string "I'll give you a little fishing advice.\p"
	.string "First, you want to face the water,\n"
	.string "then use the Rod.\p"
	.string "Focus your mind…\n"
	.string "If you get a bite, pull the Rod.\p"
	.string "Sometimes you can snag something\n"
	.string "immediately, but with bigger catches,\l"
	.string "you need to time the pulls on your Rod\l"
	.string "to haul them in.$"

DewfordTown_Text_XIsTheBiggestHappeningThingRight: @ 81EA136
	.string "I like what's hip, happening, and trendy.\n"
	.string "I'm always checking it out.\p"
	.string "Listen, have you heard about this new\n"
	.string "“{STR_VAR_1}”?\p"
	.string "That's right!\n"
	.string "Of course you know!\p"
	.string "I mean, sheesh,\n"
	.string "“{STR_VAR_1}”…\l"
	.string "It's the hottest thing in cool!\p"
	.string "Wherever you're from,\n"
	.string "“{STR_VAR_1}”\l"
	.string "is the biggest happening thing, right?$"

DewfordTown_Text_TellMeWhatsNewAndIn: @ 81EA242
	.string "Hunh?\n"
	.string "It's not the hip and happening thing?\p"
	.string "Well, hey, you have to tell me,\n"
	.string "what's new and what's “in”?$"

DewfordTown_Text_OfCourseIKnowAboutThat: @ 81EA2AA
	.string "Hunh?\n"
	.string "“{STR_VAR_2}”?\p"
	.string "… …\p"
	.string "…Uh… Yeah! That's right!\n"
	.string "Yeah, I knew that! Knew it all along!\p"
	.string "Of course I know about that!\n"
	.string "“{STR_VAR_2},” right?\p"
	.string "Yeah, that's it, it's there!\n"
	.string "Isn't “{STR_VAR_2}”\l"
	.string "the coolest, or what?\p"
	.string "It's the hippest thing in hip.\n"
	.string "You think I'd not know about it?\p"
	.string "“{STR_VAR_1}”…\n"
	.string "It's, like, so five minutes ago.\p"
	.string "Now, “{STR_VAR_2}” is\n"
	.string "what's vital and in tune with the times!$"

DewfordTown_Text_XHuhIThinkYIsCool: @ 81EA3FE
	.string "Hmm…\n"
	.string "“{STR_VAR_2},” huh?\p"
	.string "But personally, I think\n"
	.string "“{STR_VAR_1}”\l"
	.string "is what's real in cool.$"

DewfordTown_Text_HearOfAnyTrendsComeShareWithMe: @ 81EA443
	.string "Well, if you hear of any happening new\n"
	.string "trends, come share them with me, okay?$"

DewfordTown_Text_YeahDefinitionOfInRightNow: @ 81EA491
	.string "Yeah, absolutely right!\p"
	.string "“{STR_VAR_1}” is the\n"
	.string "definition of “in” right now.$"
`

script DewfordTown_RealEstatePropertyDoor {
	setvar(VAR_0x8009, PROPERTY_DEWFORD)
	goto(RyuScript_HouseDoorLocks)
}

script DewfordTown_RealEstatePropertySign {
	setvar(VAR_0x8009, PROPERTY_DEWFORD)
	goto(RyuRealEstateHouseSign)
}

script DT_Sumac {
	if (var(VAR_RYU_QUEST_GENESECT) == 60){
		msgbox("Please, let me know when its\nsafe to enter!")
		closemessage
		applymovement(5, Common_Movement_StepRight)
		waitmovement(5)
		turnobject(5, DIR_WEST)
		release
		end
	}
	if (var(VAR_RYU_QUEST_GENESECT) == 30){//player most likely lost to genesect. Allow them to retry.
		msgbox("I'm not sure how you got\nback here, but shall I reconfigure\lthe device and send you back?", MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES){clearflag(FLAG_RYU_DFT_PAST_GENESECT) warpteleport(MAP_DEWFORD_TOWN_PAST, 255, 10, 14) waitstate}
		else{goto(ryu_end)}
	}
	elif (var(VAR_RYU_QUEST_GENESECT) == 50){//player probably defeated genesect. Continue story.
		goto(DT_ContinueGenesectStoryAfterDefeat)
	}
	lockall
	faceplayer
	msgbox("You found me!\pNot that I was hiding or anything.")
	closemessage
	if (flag(FLAG_RYU_HAS_FOLLOWER) == TRUE){
		if (var(VAR_RYU_FOLLOWER_ID) == FOLLOWER_LANETTE){
			msgbox("{COLOR LIGHT_RED}{SHADOW RED} Hi, uncle Sumac.\pHow have you been?\p{COLOR DARK_GREY}{SHADOW LIGHT_GREY}I've been very well indeed.\pI'm happy to finally be able\nto carry out my work!\pI knew you were my favorite\nniece for a reason...\p{COLOR LIGHT_RED}{SHADOW RED}Oh... uh... kay...\p{COLOR LIGHT_GREEN}{SHADOW GREEN} (Lanette looks a little embarassed\n but you also sense some\l pride)")
			closemessage
		}
	}
	msgbox("Ahem! So!\pMy research indicates that the\nGenesis Pokémon lived around\lhere.\pThis hill seems to be covering\nsomething, but I don't know how\pwe could possibly excavate\nit without angering the locals.")
	closemessage
	msgbox("Hmm, quite the conundrum indeed...\p... ... ...")
	closemessage
	turnobject(5, DIR_NORTH)
	delay(30)
	playse(SE_PIN)
	applymovement(5, Common_Movement_ExclamationMark)
	waitmovement(5)
	turnobject(5, DIR_SOUTH)
	msgbox("AHA! I got it!\pWe can simply go back in\ntime and change how this\lhill formed!")
	closemessage
	goto(DT_SumacIntroP2)
}
script DT_SumacIntroP2{
	applymovement(PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}Are... you implying that time\ntravel is possible?\lNo... simple?!\pPerhaps you really are crazy...\nold man...")
	closemessage
	msgbox("Yes indeed, young one.\pI developed a device that can\nteleport you through {COLOR LIGHT_BLUE}{SHADOW RED}time {COLOR DARK_GREY}{SHADOW LIGHT_GREY}\nas well as space.")
	closemessage
	msgbox("There's a small caveat though,\nyou need to have some sort\lof anchor.\pIt's the only way the device\nknows where... and when to send you.\pHowever... you have just what\nI need!")
	closemessage
	msgbox("That drive you have!\nIt's linked to this area\land Genesis's time!")
	closemessage
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} ({PLAYER} pulls out the drive...)")
	closemessage
	msgbox("Right! just attach this device\nhere... connect that...\pand... presto!\pIt's good to go.")
	closemessage
	msgbox("Now all you have to do is press this-")
	closemessage
	setvar(VAR_RYU_QUEST_GENESECT, 20) //Player got teleported back in time to a past version of dewford. Figure out what to do
	drawfullscreenimage(SCENEBGWARP)
	delay(12)
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN}That crazy old man pressed the\nbutton without even giving me\ltime to prepare!\pThis better not screw me!")
	closemessage
	clearfullscreenimage
	warpteleport(MAP_DEWFORD_TOWN_PAST, 255, 10, 14)
	waitstate
	end
}

script DT_ContinueGenesectStoryAfterDefeat {
	end
}

script DT_DFT_ReturnFromPast {
	setvar(VAR_RYU_QUEST_GENESECT, 60)//player returned successfully from the past
	turnobject(PLAYER, DIR_NORTH)
	turnobject(FOLLOWER, DIR_NORTH)
	msgbox("Ah! you finally arrived!\pAre you ready to go in the cave?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO){
		msgbox("Come see me when you're ready!\pI'll wait here for you.")
		release
		end
	}
	closemessage
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (You notice that there's a cave door\n where there wasn't one previously.\p Sumac is acting as if it'd always\n been there.\p You must have been successful in your\n past endeavor.")
	closemessage
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}When did that cave appear?")
	closemessage
	msgbox("This cave?\nIt's always been here, what\lare you talking about?\pWho's the crazy one now?\pIt's just been a highly protected\nexcavation site.")
	closemessage
	fadescreen(FADE_TO_BLACK)
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (You tell Sumac what happened\n when you were initially sent\l to the past, and how the cave\p was definitely not there before.)")
	closemessage
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (You then describe to him\n the events that transpired\l in the past.)")
	closemessage
	delay(10)
	fadescreen(FADE_FROM_BLACK)
	msgbox("How truly fascinating!\nYou've proven that you can\lrewrite history!")
	closemessage
	msgbox("It really is something!\nThis cave is firmly inscribed\pinto my memory as a long\nknown excavation site!")
	closemessage
	msgbox("My dear boy, you have proven\na whole new branch of science needs\lto exist!")
	closemessage
	msgbox("You must now return the device.\pI need to check logs and status.\p{COLOR LIGHT_GREEN}{SHADOW GREEN} (You hand over the device.)")
	closemessage
	msgbox("Right! On with you!\pLet me know when it's safe to enter!")
	closemessage
	applymovement(5, Common_Movement_StepRight)
	waitmovement(5)
	turnobject(5, DIR_WEST)
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (The way is clear for you\n to proceed.)")
	closemessage
	release
	end
}