mapscripts MauvilleCity_FrontierBank_MapScripts {}

//Female banker person

script SCFB_Teller{
    msgbox("Hi! Welcome to Frontier Bank.\nHow can I help you today?")
    if (!flag(FLAG_RYU_PLAYER_HAS_BANK_ACCOUNT)) {
        goto(SCFB_MakeAccount)
    }
    multichoicedefault(0, 4, MULTI_FRONTIERBANK, 0, 0)
    switch(var(VAR_RESULT)) {
        case 0:
            msgbox("Do you wish to make a deposit?", MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES) {
                    msgbox(format("How much would you like to deposit?"))
                    numberentry(0, 10, 9)
                    closemessage
                    specialvar(VAR_TEMP_2, RyuFBDoDeposit)
                    switch (var(VAR_TEMP_2)) {
                    case 0:
                        msgbox("One moment please...")
                        closemessage
                        msgbox("You do not have enough money on\nyour person to deposit that amount.")
                        closemessage
                        goto(SCFB_Teller)
                    case 1:
                        msgbox(OkayWeDeposit)
                        closemessage
                        msgbox("We've deposited ¥{STR_VAR_1}\n into your account.\pYour account now has a balance\nof ¥{STR_VAR_2}.\lHave a nice day!")
                        release
                        end
                    case 2:
                        msgbox("One moment please...")
                        msgbox(format("That deposit would put you past the limit of ¥2,000,000,000 per account. We cannot process this transaction. Sorry."))
                        goto(SCFB_Teller)
                    case 127:
                        msgbox("Okay.") 
                        goto(SCFB_Teller)
                    }
                }
                else {
                    msgbox(format("Okay."))
                    goto(SCFB_Teller)
                }
        case 1:
            msgbox(format("So, you would like to make a withdrawal?"), MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES){
                    msgbox(format("So, how much would you like to withdraw?"))
                    numberentry(0, 10, 9)
                    closemessage
                    msgbox("One moment please...")
                    specialvar(VAR_TEMP_2, RyuFBDoWithdraw)
                    switch (var(VAR_TEMP_2)) {
                        case 0:
                            msgbox("You do not have enough money\nin your account to withdraw\lthat amount.")
                            closemessage
                            goto(SCFB_Teller)
                        case 1:
                            msgbox("We've withdrawn ¥{STR_VAR_1}\nfrom your account.\pYour account now has a balance\nof ¥{STR_VAR_2}.\lHave a nice day!")
                            closemessage
                            release
                            end
                        case 2:
                            msgbox(format("The withdrawal amount would exceed the amount of money a single person can carry. We can't process this withdrawal. Sorry."))
                            closemessage
                            release
                            end
                        case 3:
                            msgbox(format("You didn't choose an amount!"))
                            goto(SCFB_Teller)
                        case 4:
                            msgbox("Okay!\pHere's ¥{STR_VAR_1},\nthe amount required to fill your\lpersonal wallet.\pYou have a remaining balance of\n¥{STR_VAR_2}.\pHave a nice day!")
                            closemessage
                            release
                            end
                        case 127:
                            msgbox("Okay.")
                            goto(SCFB_Teller)
                    }
                }
                else {
                    msgbox(format("Okay."))
                    goto(SCFB_Teller)
                }
        case 2:
            special(RyuBufferBankBalance)
            msgbox("Your account has a balance\nof ¥{STR_VAR_1}.")
            goto(SCFB_Teller)
    }
    release
    end
}

text OkayWeDeposit {
    "Okay, we'll deposit that money\n"
    "for you.\p"
    "One moment please."
}

script SCFB_MakeAccount {
    msgbox("Ah! So you would like to\nset up a new account?\pLet me get this set up for you.\pCurrently, our interest rate is 0.5%\nevery 24 Real-Time hours.\p...\pOkay! All set!")
    setflag(FLAG_RYU_PLAYER_HAS_BANK_ACCOUNT)
    goto(SCFB_Teller)
}

script SCFB_Manager{
    if (flag(FLAG_RYU_NOBLES_FINAL_QUEST_IN_PROGRESS) == TRUE){
        checkmoney(1000000)
        if (var(VAR_RESULT) == TRUE) {
            msgbox("Oh, my.\nYou have considerable wealth.\pYou should consider getting into\nthe real estate business.\pWe own many properties throughout\nHoenn.\pI'll put you in our realtor registry\nso that you can manage properties.\pJust remember that the Frontier Bank\nalways gets a cut.")
            callnative(SetPlayerRealtorStatus)
            msgbox("You may now buy, sell, rent, or use\nproperties as you like.\pI look forward to a profitable\nrelationship.")
            clearflag(FLAG_RYU_NOBLES_FINAL_QUEST_IN_PROGRESS)
            setflag(FLAG_RYU_COMPLETED_NOBLES_FINAL_QUEST)
            release
            end
        }
        else {
            msgbox("You've done a lot to assist our\njunior board members.\pHowever, despite their\nrecommendation,\lI cannot allow you participate in\pour business unless you have enough\npersonal wealth to buy in.\pIt's not a charity.\nReturn to me when you have become\la millionaire,\land we'll talk.")
            closemessage
            release
            end
        }
    }
    msgbox("Hello, there!\pI'm the manager of this here\nfine establishment.\pWe offer the best interest rates\naround!\pMake an account today!")
    closemessage
    release
    end
}
