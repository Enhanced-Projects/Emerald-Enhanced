raw `
LittlerootTown_BrendansHouse_1F_MapScripts:: @ 81F7755
	map_script ON_FRAME, LRT_MayIntroCheck
    .byte 0

LRT_MayIntroCheck::
	map_script_2 VAR_TEMP_5, 0, LRT_MayIntroEvent
	.2byte 0


LRT_leftSideTrigger::
	goto_if_unset FLAG_SYS_GAME_CLEAR, ryu_end
	applymovement PLAYER, Lrt_trigger_moveRight
	waitmovement 0
	goto LRT_GameClearTriggerMale
	
LRT_rightSideTrigger::
	goto_if_unset FLAG_SYS_GAME_CLEAR, ryu_end
	applymovement PLAYER, Lrt_trigger_moveLeft
	waitmovement 0
	goto LRT_GameClearTriggerMale
	
LRT_GameClearTriggerMale::
	call LRT_maleRivalDSStage3
	checkplayergender
	compare VAR_RESULT, FEMALE
	goto_if_eq ryu_end
	goto_if_set FLAG_RECEIVED_SS_TICKET, ryu_end
	goto_if_unset FLAG_SYS_GAME_CLEAR, ryu_end
	applymovement 1, LRT_MalePlayerMomMeetPlayer
	waitmovement 0
	msgbox LRT_MomGratz MSGBOX_DEFAULT
	closemessage
	applymovement 3, LRT_MaleplayerDadGratz
	waitmovement 0
	msgbox LRT_MalePlayerDadMsg MSGBOX_DEFAULT
	closemessage
	additem ITEM_SS_TICKET
	setflag FLAG_RECEIVED_SS_TICKET
	setflag FLAG_HIDE_BH_DAD
	applymovement 3, LRT_MaleDadLeave
	waitmovement 0
	playse SE_DOOR
	removeobject 3
	release
	end
	
Lrt_trigger_moveRight:
	walk_right
	walk_in_place_down
	emote_question_mark
	step_end
	
Lrt_trigger_moveLeft:
	walk_left
	walk_in_place_down
	emote_question_mark
	step_end
	
LRT_MalePlayerMomMeetPlayer:
	walk_up
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_up
	walk_in_place_left
	step_end
	
LRT_MaleplayerDadGratz:
	walk_up
	walk_right
	walk_right
	walk_up
	walk_in_place_right
	step_end
	
LRT_MaleDadLeave:
	walk_down
	walk_down
	walk_down
	walk_right
	walk_down
	step_end
	
LRT_MalePlayerDadMsg:
	.string "Dad: I... I can't believe it... \pYou succeeded where I failed.\nYou became Master, while I settled for\lLeader.\pI'm so very proud of you!\pI have a gift for you.\nTake this ticket and go to the\lBattle Fronter.\pYou can truly test your skill there!$"

LRT_maleRivalDSStage3::
	goto_if_set FLAG_RYU_DS_MALE_MOM_MET, ryu_end
	goto_if_set FLAG_RYU_DS_BRENDAN_PARTNERS, LRT_MRDSS3
	return
	
LRT_MRDSS3::	
	applymovement FOLLOWER, Common_Movement_StepRight
	waitmovement FOLLOWER
	applymovement FOLLOWER, Common_Movement_FaceDown
	waitmovement FOLLOWER
	applymovement 2, LRT_MeetFemalePlayer
	waitmovement 0
	applymovement PLAYER, LRT_facedown
	msgbox LRT_maleRivalMomCuteCouple MSGBOX_DEFAULT
	setflag FLAG_RYU_DS_MALE_MOM_MET
	applymovement 2, LRT_ReturnToTable2
	waitmovement 0
	return

LRT_maleRivalMomCuteCouple:
	.string "You've been spending an awful lot\nof time with my {RIVAL}.\pDon't worry, both me and the Professor\nthink you make a cute couple.\pI'm counting on you to take care\n of my {RIVAL}!$"

LRT_MeetFemalePlayer:
	walk_up
	walk_up
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_in_place_up
	step_end
	
LRT_ReturnToTable2:

	walk_down
	walk_down
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	walk_in_place_Right
	step_end

@@=================== Warp Zone ===================@@

WarpZone_EventScript_Slakoth::
	msgbox WarpZone_Text_WELCOME, MSGBOX_NPC
	end

WarpZone_Text_WELCOME:
	.string "WELCOME TO WARP ZONE!$"

WarpZone_EventScript_Sign1::
	msgbox WarpZone_Text_Sign1, MSGBOX_SIGN
	end

WarpZone_Text_Sign1:
	.string "Mirage Cave$"

WarpZone_EventScript_Sign2::
	msgbox WarpZone_Text_Sign2, MSGBOX_SIGN
	end

WarpZone_Text_Sign2:
	.string "Basalt Burg$"

WarpZone_EventScript_Sign3::
	msgbox WarpZone_Text_Sign3, MSGBOX_SIGN
	end

WarpZone_Text_Sign3:
	.string "Safari Zone$"

WarpZone_EventScript_Sign4::
	msgbox WarpZone_Text_Sign4, MSGBOX_SIGN
	end

WarpZone_Text_Sign4:
	.string "$"

`
script LRT_RivalMother {
	lock
	faceplayer
	if (var(VAR_RYU_QUEST_MAY) >= 5) {
		if (flag(FLAG_RYU_MAY_EVENT_FAILED) == TRUE) {
			goto(LRT_RivalMom_Afterstory)
		}
		if (var(VAR_RYU_QUEST_MAY) >= 10) {
			goto(LRT_RivalMom_Afterstory)
		}
		msgbox("Oh, hi!\pYou've done so much work for\nmy husband.\pInstead of letting his research take\nhim over, He finally took me on\lvacation!\pWe met this lovely girl while we\nwere travelling and found out that\lher parents were gone!\pThe professor decided to adopt her.\nIsn't that great?!")
		closemessage
		setvar(VAR_RYU_QUEST_MAY, 10) //Player talked to may's adopted mother, find may in littleroot.
		clearflag(FLAG_RYU_DS_MAY_ACQ)
		release
		end
	}
	msgbox("Oh, hi, {PLAYER}.\pWere you looking for my husband?\pHe's in the lab... like usual.\lWish he would come home.")
	closemessage
	release
	end
}

script LRT_RivalMom_Afterstory {
	if (flag(FLAG_RYU_RIVALMOM_OUTRO) == FALSE) {
		msgbox("Hi, {PLAYER}!\pMy husband is home more often now,\nthanks to you!\pYou saved our marriage, and even\nmade it possible for us to give a\lhome to a girl in need.\pIf you need anything at all,\nlet me know!")
		setflag(FLAG_RYU_RIVALMOM_OUTRO)
	}
	msgbox("How's it going, {PLAYER}?!\pDid you need something?")
	release
	end
}

script LRT_MayIntroEvent { 
	if(flag(FLAG_RYU_DS_MAY_FRIENDS) == FALSE) {
		setvar(VAR_TEMP_5, 100)
		release
		end
	}
	elif ((flag(FLAG_RYU_HAS_FOLLOWER) == FALSE) || (!(var(VAR_RYU_FOLLOWER_ID) == OBJ_EVENT_GFX_MAY))) {
		setvar(VAR_TEMP_5, 100)
		release
		end
	}
	if (var(VAR_RYU_QUEST_MAY) == 25) { //may intro event done
		setvar(VAR_TEMP_5, 100)
		release
		end
	}
	lock
	waitmovement(CAMERA)
	applymovement(FOLLOWER, LRT_BH_MayNextToPlayer)
	waitmovement(FOLLOWER)
	applymovement(2, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(2)
	applymovement(2, Common_Movement_FaceDown)
	waitmovement(2)
	msgbox("Oh, she's arrived!\pBIRCH!!! She's HERE!\nGet DOWN HERE!")
	closemessage
	special(SpawnCameraObject)
	applymovement(CAMERA, LRT_MayEvent_Cam1)
	playse(SE_DOOR)
	addobject(7)
	applymovement(7, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(7)
	applymovement(7, Common_Movement_StepDown)
	waitmovement(7)
	applymovement(7, Common_Movement_StepLeft)
	waitmovement(7)
	turnobject(7, DIR_SOUTH)
	msgbox(LRT_BirchMaySpeech)
	closemessage
	if (flag(FLAG_RYU_DS_DAWN_PARTNERS) == TRUE) {
		call(LRT_BH_DawnEnter)
	}
	setvar(VAR_RYU_QUEST_MAY, 25) //Player completed may event in rival's house.
	applymovement(FOLLOWER, LRT_BH_MayToStairs)
	waitmovement(FOLLOWER)
	applymovement(7, Common_Movement_StepLeft)
	waitmovement(7)
	applymovement(7, Common_Movement_FaceDown)
	waitmovement(7)
	checkplayergender
	if (var(VAR_RESULT) == MALE) {
		applymovement(FOLLOWER, Common_Movement_StepRight)
		waitmovement(FOLLOWER)
	}
	applymovement(FOLLOWER, Common_Movement_FaceDown)
	waitmovement(FOLLOWER)
	msgbox("May:{COLOR LIGHT_RED}{SHADOW RED} Okay... Thanks, {PLAYER}!\nI'm going to go get unpacked now.\pCome visit later if you wanna\nhang out!")
	closemessage
	applymovement(FOLLOWER, Common_Movement_StepUp)
	waitmovement(FOLLOWER)
	playse(SE_DOOR)
	destroyfollower
	clearflag(FLAG_RYU_BH_HIDE_MAY)
	setflag(FLAG_RYU_DS_MAY_CLOSEFRIENDS)
	msgbox("Birch: Okay! That's all settled.\nI have some work to attend to,\lthen I'll be back home, honey!")
	applymovement(7, LRT_MAYEVENT_BirchExit)
	waitmovement(7)
	removeobject(7)
	applymovement(8, Common_Movement_StepLeft)
	waitmovement(8)
	applymovement(8, Common_Movement_StepUp)
	waitmovement(8)
	playse(SE_DOOR)
	removeobject(8)
	applymovement(CAMERA, LRT_MayEvent_Cam2)
	waitmovement(CAMERA)
	special(RemoveCameraObject)
	release
	end
}

script LRT_BH_DawnEnter {
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_RIVAL_DAWN_NORMAL)
	addobject(8)
	applymovement(8, Common_Movement_StepDown)
	waitmovement(8)
	applymovement(8, Common_Movement_StepRight)
	waitmovement(8)
	turnobject(8, DIR_SOUTH)
	checkplayergender
	if (var(VAR_RESULT) == MALE) {
		msgbox("{RIVAL}:{COLOR LIGHT_RED}{SHADOW RED} How is it that you have\nmet and already wooed my sister\pbefore I even met her?\pYou're such a stud!")
	}
	else {
		msgbox("{RIVAL}:{COLOR LIGHT_RED}{SHADOW RED} Wow, you met my new sister even\nbefore I did,\pand already have her falling for you.\n Impressive!")
	}
	closemessage
	applymovement(7, Common_Movement_FaceLeft)
	applymovement(FOLLOWER, Common_Movement_FaceLeft)
	waitmovement(FOLLOWER)
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (May and her new father both blush)")
	applymovement(FOLLOWER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	msgbox("May: {COLOR LIGHT_RED}{SHADOW RED}Th-\nThat's not true!")
	closemessage
	applymovement(FOLLOWER, Common_Movement_FaceUp)
	waitmovement(FOLLOWER)
	applymovement(8, Common_Movement_FaceRight)
	waitmovement(8)
	applymovement(8, Common_Movement_FaceDown)
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} ({RIVAL} chuckles...)")
	closemessage
	msgbox("Rival's Mom: Well anyways,\nWelcome home, dear!\pI'm guessing Birch already set\nup your new room.")
	closemessage
	msgbox("Birch: Ahem. Yes, that's what I was\ndoing.\pIt's all good to go!")
	setflag(FLAG_RYU_COMPLETED_MAY_INTRO_WITH_DAWN) //to be used later for a special scene if player romanced dawn before may.
	closemessage
	return
}

movement LRT_MAYEVENT_BirchExit {
	walk_down * 4
	walk_right
	walk_down
	step_end
}

movement LRT_BH_MayNextToPlayer {
	walk_up
	walk_left
	face_up
	step_end
}

movement LRT_MayEvent_Cam1 {
	walk_up * 2
	step_end
}

movement LRT_MayEvent_Cam2 {
	walk_down * 2
	step_end
}

text LRT_BirchMaySpeech {
	"Birch: Oh! Hi, {COLOR LIGHT_RED}{SHADOW RED}May{COLOR DARK_GREY}{SHADOW LIGHT_GREY}!\p"
	"I'm glad you found us okay!\p"
	"{COLOR LIGHT_RED}{SHADOW RED}{PLAYER} here helped me find the right\nhouse."
}

movement LRT_BH_MayToStairs {
	walk_up * 4
	step_end
}