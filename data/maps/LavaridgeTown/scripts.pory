raw `
LavaridgeTown_MapScripts:: @ 81EA4D3
	map_script ON_TRANSITION, LavaridgeTown_OnTransition
	.byte 0

LavaridgeTown_OnTransition: @ 81EA4DE
	setflag FLAG_VISITED_LAVARIDGE_TOWN
	call RyuCheckForWellTravelledAch
	call_if_set FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY, LavaridgeTown_EventScript_ShowMtChimneyTrainers
	call Common_EventScript_SetupRivalGfxId
	call RyuCheckLavaridgeGiftStatus
	end

LavaridgeTown_EventScript_ShowMtChimneyTrainers:: @ 81EA53F
	clearflag FLAG_HIDE_MT_CHIMNEY_TRAINERS
	return

LavaridgeTown_EventScript_HotSpringsTrigger:: @ 81EA6FA
	specialvar VAR_RESULT, GetPlayerFacingDirection
	compare VAR_RESULT, DIR_SOUTH
	goto_if_eq LavaridgeTown_EventScript_EnteredHotSprings
	end

LavaridgeTown_EventScript_EnteredHotSprings:: @ 81EA70B
	incrementgamestat GAME_STAT_ENTERED_HOT_SPRINGS
	end

LavaridgeTown_EventScript_ExpertM:: @ 81EA70E
	msgbox LavaridgeTown_Text_HotSpringsNeverRunDry, MSGBOX_NPC
	end

LavaridgeTown_EventScript_OldMan:: @ 81EA717
	msgbox LavaridgeTown_Text_PokemonNippedBackside, MSGBOX_SIGN
	end

LavaridgeTown_EventScript_Twin:: @ 81EA720
	msgbox LavaridgeTown_Text_BatheInHotSpringsEveryDay, MSGBOX_NPC
	end

LavaridgeTown_EventScript_HotSpringsOldWoman1:: @ 81EA729
	msgbox LavaridgeTown_Text_IfPokemonInHotSprings, MSGBOX_NPC
	end

LavaridgeTown_EventScript_HotSpringsOldWoman2:: @ 81EA732
	msgbox LavaridgeTown_Text_HotSpringsClaims, MSGBOX_NPC
	end

LavaridgeTown_EventScript_ExpertF:: @ 81EA73B
	msgbox LavaridgeTown_Text_OhYouLikeHotSprings, MSGBOX_NPC
	end

LavaridgeTown_EventScript_EggWoman:: @ 81EA744
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_LAVARIDGE_EGG, LavaridgeTown_EventScript_ReceivedEgg
	msgbox LavaridgeTown_Text_HaveEggWillYouTakeIt, MSGBOX_YESNO
	compare VAR_RESULT, NO
	goto_if_eq LavaridgeTown_EventScript_DeclineEgg
	getpartysize
	compare VAR_RESULT, PARTY_SIZE
	goto_if_eq LavaridgeTown_EventScript_NoRoomForEgg
	msgbox LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg, MSGBOX_DEFAULT
	setflag FLAG_RECEIVED_LAVARIDGE_EGG
	playfanfare MUS_FANFA4
	message LavaridgeTown_Text_ReceivedTheEgg
	waitfanfare
	giveegg SPECIES_LARVESTA
	release
	end

LavaridgeTown_EventScript_ReceivedEgg:: @ 81EA787
	msgbox LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_NoRoomForEgg:: @ 81EA791
	msgbox LavaridgeTown_Text_NoRoomForThisEgg, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_DeclineEgg:: @ 81EA79B
	msgbox LavaridgeTown_Text_AsYouWishThen, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_TownSign:: @ 81EA7A5
	msgbox LavaridgeTown_Text_TownSign, MSGBOX_SIGN
	end

LavaridgeTown_EventScript_GymSign:: @ 81EA7AE
	msgbox LavaridgeTown_Text_GymSign, MSGBOX_SIGN
	end

LavaridgeTown_EventScript_HerbShopSign:: @ 81EA7B7
	msgbox LavaridgeTown_Text_HerbShopSign, MSGBOX_SIGN
	end

LavaridgeTown_Text_HaveEggWillYouTakeIt: @ 81EAB80
	.string "I have here an Egg.\p"
	.string "I'd hoped to hatch it by covering it in\n"
	.string "hot sand by the hot springs.\l"
	.string "But that doesn't seem to be enough…\p"
	.string "I've heard it would be best if it were\n"
	.string "kept together with Pokémon and\l"
	.string "carried about.\p"
	.string "You are a Trainer, yes?\n"
	.string "And your Pokémon radiate vitality.\p"
	.string "So, what say you?\n"
	.string "Will you take this Egg to hatch?$"

LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg: @ 81EACC0
	.string "Good! I hope you'll walk plenty with\n"
	.string "this here Egg!$"

LavaridgeTown_Text_ReceivedTheEgg: @ 81EACF4
	.string "{PLAYER} received the EGG.$"

LavaridgeTown_Text_NoRoomForThisEgg: @ 81EAD09
	.string "Oh? You've too many Pokémon.\n"
	.string "There's no room for this Egg…$"

LavaridgeTown_Text_AsYouWishThen: @ 81EAD44
	.string "As you wish, then…\p"
	.string "If you have a change of heart about\n"
	.string "hatching this Egg, I will be here.$"

LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare: @ 81EAD9E
	.string "Every so often, an Egg will be found at\n"
	.string "the Pokémon Day Care.\p"
	.string "Or at least that's how the rumor goes.$"

LavaridgeTown_Text_HotSpringsNeverRunDry: @ 81EAE03
	.string "We draw as much hot water as we need,\n"
	.string "and yet the hot springs never run dry.\p"
	.string "Isn't it magical?\p"
	.string "These hot springs appear near active\n"
	.string "volcanoes. Veins of water under the\l"
	.string "ground are heated by magma to well up\l"
	.string "as hot springs.$"

LavaridgeTown_Text_PokemonNippedBackside: @ 81EAEE1
	.string "Being buried in this hot sand is…\n"
	.string "Sigh…\p"
	.string "So warm and heavenly…\p"
	.string "Eh? Gyaah! Ouch!\p"
	.string "A Pokémon nipped my backside!$"

LavaridgeTown_Text_OhYouLikeHotSprings: @ 81EAF4E
	.string "Oh, you like hot springs, do you?\p"
	.string "That's surprising for one as young\n"
	.string "as you.$"

LavaridgeTown_Text_BatheInHotSpringsEveryDay: @ 81EAF9B
	.string "I bathe in the hot springs every day.\p"
	.string "I want to become a beautiful and strong\n"
	.string "Gym Leader like Flannery.$"

LavaridgeTown_Text_IfPokemonInHotSprings: @ 81EB003
	.string "If people put Pokémon in hot springs,\n"
	.string "it might be seriously strange.\p"
	.string "Why, it might be an electric bath, or\n"
	.string "a bubble bath, or even a lava bath…$"

LavaridgeTown_Text_HotSpringsClaims: @ 81EB092
	.string "They're claiming that these hot springs\n"
	.string "are good for calming nervous tension,\l"
	.string "relieving aching muscles, solving\l"
	.string "romantic problems, and attracting\l"
	.string "money…$"

LavaridgeTown_Text_TownSign: @ 81EB12B
	.string "Lavaridge Town\p"
	.string "“Pokémon Center Hot Springs\n"
	.string "An excellent place for relaxing!”$"

LavaridgeTown_Text_GymSign: @ 81EB178
	.string "Lavaridge Town Pokémon Gym\n"
	.string "Leader: Flannery\l"
	.string "“One with a fiery passion that burns!”$"

LavaridgeTown_Text_HerbShopSign: @ 81EB1CB
	.string "Pokémon Herb Shop\n"
	.string "“Bitter taste--better cure!”$"

`
script LT_CheckCanReceiveGift {
	lock
	faceplayer
	if ((flag(FLAG_RYU_DAWN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_RYU_BRENDAN_GIFTPOKE_RECEIVED) == TRUE)) {
		goto(ryu_end)
	}
	specialvar(VAR_RESULT, CalculatePlayerPartyCount)
	if (var(VAR_RESULT) < 6) {
		goto(LT_DoGiftSegment)
	}
	else {
		msgbox("{COLOR LIGHT_RED}{SHADOW RED}You still don't have space!\nCome back to me when you do!")
		release
		end
	}
}
script LT_RivalEnter3{
	if ((flag(FLAG_RYU_DAWN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_RYU_BRENDAN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_DEFEATED_LAVARIDGE_GYM) == FALSE) || (flag(FLAG_RYU_COMPLETED_LAVARIDGE_EVENT) == TRUE)) {
		setvar(VAR_TEMP_0, 100)
		goto(ryu_end)
	}
	applymovement(PLAYER, Common_Movement_StepLeft)
	waitmovement(PLAYER)
	turnobject(PLAYER, DIR_NORTH)
	goto(LT_RivalEnter)
}

script LT_RivalEnter2{
	if ((flag(FLAG_RYU_DAWN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_RYU_BRENDAN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_DEFEATED_LAVARIDGE_GYM) == FALSE)|| (flag(FLAG_RYU_COMPLETED_LAVARIDGE_EVENT) == TRUE)) {
		setvar(VAR_TEMP_0, 100)
		goto(ryu_end)
	}
	applymovement(PLAYER, Common_Movement_StepRight)
	waitmovement(PLAYER)
	turnobject(PLAYER, DIR_NORTH)
	goto(LT_RivalEnter)
}

script LT_RivalEnter {
	if ((flag(FLAG_RYU_DAWN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_RYU_BRENDAN_GIFTPOKE_RECEIVED) == TRUE) || (flag(FLAG_DEFEATED_LAVARIDGE_GYM) == FALSE)|| (flag(FLAG_RYU_COMPLETED_LAVARIDGE_EVENT) == TRUE)) {
		setvar(VAR_TEMP_0, 100)
		goto(ryu_end)
	}
	setflag(FLAG_RYU_COMPLETED_LAVARIDGE_EVENT)
	setvar(VAR_TEMP_0, 100)
	addobject(7)
	applymovement(7, LT_RivalMeetPlayer)
	waitmovement(7)
LT_DoGiftSegment:
	lock
	faceplayer
	checkplayergender
	if (var(VAR_RESULT) == 0) {
		goto(LT_FemaleRivalGiftScene)
	}
	else {
		goto(LT_MaleRivalGiftScene)
	}
}

script LT_MaleRivalGiftScene {
	drawfullscreenimage(SCENEBGBRENDAN)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Heya neighbor!\pI see you completed the Lavaridge\nGym!\pThat fight was tough for me, for sure.\pSpeaking of tough, I have a pokémon\nthat's not tough at all!\pIt seems to need a more\ngentle approach for training.")
	closemessage
	clearfullscreenimage
	showmonpic(SPECIES_SNORUNT, 4, 4)
	playmoncry(SPECIES_SNORUNT, 0)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}I wanted to evolve this pokémon\ninto a Froslass, but my training\lmethods don't work.\pFroslass is a pretty pokemon\nso I think having a pretty trainer\lwill work!")
	hidemonpic
	specialvar(VAR_RESULT, CalculatePlayerPartyCount)
	if (var(VAR_RESULT) < 6) {
		special(RyuBrendanGiftPoke)
		setflag(FLAG_RYU_BRENDAN_GIFTPOKE_RECEIVED)
		goto(LT_FinishGiftEvent)
	}
	else{
		msgbox("{COLOR LIGHT_RED}{SHADOW RED}Hey! I want you to take this\npokémon, but you don't have space!\pCome back with space!")
		clearflag(FLAG_RYU_HIDE_LAVARIDGE_RIVAL)
		goto(ryu_end)
	}
}

script LT_FemaleRivalGiftScene {
	drawfullscreenimage(SCENEBGDAWN)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Hi, {PLAYER}!\pFancy meeting you here!\pIt looks like you beat Flannery.\nShe was pretty tough for me.\pGlad to see you finished though!")
	closemessage
	clearfullscreenimage
	showmonpic(SPECIES_SNEASEL, 4, 4)
	playmoncry(SPECIES_SNEASEL, 0)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Anyways... I've got a Sneasel here\nthat doesn't listen very well.\pI was hoping perhaps you could\ntake it and train it.\pIt's too bull-headed for me.")
	hidemonpic
	specialvar(VAR_RESULT, CalculatePlayerPartyCount)
	if (var(VAR_RESULT) < 6) {
		special(RyuDawnGiftPoke)
		setflag(FLAG_RYU_DAWN_GIFTPOKE_RECEIVED)
		goto(LT_FinishGiftEvent)
	}
	else{
		msgbox("{COLOR LIGHT_RED}{SHADOW RED}Huh?! You don't have space for\nSneasel!\pCome back to me when you have\nspace!")
		clearflag(FLAG_RYU_HIDE_LAVARIDGE_RIVAL)
		goto(ryu_end)
	}
}

script LT_FinishGiftEvent {
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Okay! thanks for taking care of\nthe pokémon!\pLooking forward to seeing you\nboth grow!")
	applymovement(7, LT_RivalExitLavaridge)
	waitmovement(7)
	setflag(FLAG_RYU_HIDE_LAVARIDGE_RIVAL)
	removeobject(7)
	release
	end
}

script RyuCheckLavaridgeGiftStatus {
	if (flag(FLAG_RYU_HIDE_LAVARIDGE_RIVAL) == FALSE) {
		setobjectxyperm(7, 9, 12)
		return
	}
	return
}

movement LT_RivalMeetPlayer {
	walk_down * 3
	walk_left
	walk_down * 2
	step_end
}

movement LT_RivalExitLavaridge {
	walk_up * 2
	walk_right * 9
	step_end
}
const LUCY = 10

script LT_CheckLucyQuest{
	if (flag(FLAG_RYU_LAVARIDGE_LUCY) == TRUE){
		goto(ryu_end)
	}
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (You see a beautiful girl\n relaxing in the hot spring.)")
	delay(60)
	applymovement(LUCY, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitse
	applymovement(LUCY, Common_Movement_StepDown)
	waitmovement(LUCY)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Umm, can I help you?")
	closemessage
	bufferdynamicmulti(LT_lucy1, LT_lucy2)
	multichoice(0, 10, MULTI_DYNAMIC_2, 1)
	if (var(VAR_RESULT) == 0){ //end quest
		msgbox("{COLOR LIGHT_RED}{SHADOW RED}Yes, yes, I've heard it a million\ntimes. Please don't try any\lterrible pickup lines.")
		closemessage
		applymovement(LUCY, LT_LucyLeave)
		waitmovement(LUCY)
		setvar(VAR_RYU_QUEST_LUCY, 666)//Player offended lucy. Bad End.
		removeobject(LUCY)
		setflag(FLAG_RYU_LAVARIDGE_LUCY)
		msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (Must have been something I said...)")
		closemessage
		release
		end
	}
	setvar(VAR_RYU_QUEST_LUCY, 10)//@player successfully struck conversation with Lucy. Quest begins.
	drawfullscreenimage(SCENEBGLUCY)
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}It's no problem. I'm used to it.\nAt least you were man enough\lto admit your mistake.")
	closemessage
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}I'm Lucy. What's your name?")
	closemessage
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}I'm {PLAYER}. Nice to meet you.\pyour own private hot spring, huh?\nmust be really nice.")
	closemessage
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Yeah, it cost me quite a bit to\nhave it installed.\pIt was totally worth it though,\nbecause it really helps my skin\lcare routine.")
	closemessage
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}It's really working.\pYour skin is silky and beautiful.")
	closemessage
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (You notice some red flush across\n her pearlescent face)")
	closemessage
	turnobject(LUCY, DIR_NORTH)
	waitmovement(LUCY)
	delay(30)
	turnobject(LUCY, DIR_SOUTH)
	msgbox("{COLOR LIGHT_GREEN}{SHADOW GREEN} (She clears her throat.)\p... ... ...\p{COLOR LIGHT_RED}{SHADOW RED}You're a brave one.\nI appreciate your confidence.\p Give me a minute to get dressed\nand I'll come out and we can have\la proper conversation.\pThere might be something you can\nhelp me with.")
	closemessage
	clearfullscreenimage
	applymovement(LUCY, LT_LucyLeave)
	waitmovement(LUCY)
	applymovement(LUCY, Common_Movement_SetInvisible)
	waitmovement(LUCY)
	applymovement(LUCY, LT_LucyToDoor)
	waitmovement(LUCY)
	playse(SE_DOOR)
	applymovement(LUCY, Common_Movement_StepDown)
	waitmovement(LUCY)
	turnobject(PLAYER, DIR_EAST)
	delay(90)
	applymovement(PLAYER, LT_PlayerToLucy)
	waitmovement(PLAYER)
	turnobject(LUCY, DIR_WEST)
	setvar(VAR_RYU_QUEST_LUCY, 20)//Scene in private hot spring done. Await lucy to come out front.
	setflag(FLAG_RYU_DS_LUCY_FRIENDS)
	goto(LT_LucyQuestStart)
}

script LT_LucyQuestStart{
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Have you captured a Skitty?\pWell, it doesn't matter.\nYou seem to be a well informed\ltrainer.\pYou probably know that recently\nthere's a boom in popularity of them\pbecause some people found it's\nhidden potential.")
	closemessage
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}Skitty are so popular in fact,\nthat there's a surge in poaching.\pI've been trying to track down\nthe people doing this, so that\lI can bring them to justice.")
	closemessage
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}I thought that poaching died out back\nwhen team Rocket was disbanded.\pPretty much haven't heard anything\nabout it since.")
	closemessage
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}That's correct. You ARE well\ninformed.\pNo one's really sure who's doing\nthe poaching, but there's a huge\lblack market.\pSOMEONE has to be doing something.")
	closemessage
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}And you... want me to help\nwith this task?")
	closemessage
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}I would, yes. You seem plenty\ncapable.\pThe only lead I have is that\nthere's someone in Petalburg Woods\lwho's been acting as a lookout.")
	closemessage
	msgbox("{COLOR LIGHT_RED}{SHADOW RED}It's probably the last person\nyou'd expect.\pMeet me there?")
	closemessage
	msgbox("{COLOR LIGHT_BLUE}{SHADOW BLUE}Count me in!\pI'll meet you there.")
	closemessage
	applymovement(LUCY, LT_LucyExitStageRight)
	waitmovement(LUCY)
	removeobject(LUCY)
	setflag(FLAG_RYU_LAVARIDGE_LUCY)
	setvar(VAR_RYU_QUEST_LUCY, 30) //@player has officially begun Lucy's investigation. Head to petalburg woods.
	release
	end
}

script LT_Skitty{
	faceplayer
	lock
	msgbox("Skitty: {COLOR LIGHT_RED}{SHADOW RED}Mew!")
	closemessage
	release
	end
}

movement LT_LucyLeave{
	walk_right
	walk_up * 3
	walk_right
	walk_up
	walk_right * 4
	walk_down
	step_end
}

movement LT_LucyToDoor{
	walk_down * 4
	set_visible
	step_end
}

movement LT_PlayerToLucy{
	walk_right * 5
	step_end
}

movement LT_LucyExitStageRight{
	walk_right * 2
	walk_down * 2
	walk_right * 5
	step_end 
}

text LT_lucy1{
	"{COLOR LIGHT_GREEN}{SHADOW GREEN}(Comment on her beauty)"
}

text LT_lucy2{
	"{COLOR LIGHT_GREEN}{SHADOW GREEN}(Apologize for peeping)"
}

script LT_LucyDoorClosed {
	lock
	msgbox("The door appears to be locked.")
	release
	end
}
